{-# OPTIONS --safe #-}

open import Prelude
open import FreeMonad.PackagedTheory
open import Signatures
open import FinitenessConditions

module FreeMonad.Combinations.Tensor
  {â„“}
  (Tâ‚ : FullTheory â„“)
  (Tâ‚‚ : FullTheory â„“)
  where

import FreeMonad.Combinations.Sum Tâ‚ Tâ‚‚ as SumTheory

open import FreeMonad.Combinations.Sum Tâ‚ Tâ‚‚
  hiding (ğ’¯; finVars; liftâ‚; liftâ‚‚; combinedTheory)
  public

open FullTheory Tâ‚ renaming (ğ’¯ to ğ’¯â‚; finiteArity to finArâ‚; finiteVars to finVarâ‚)
open FullTheory Tâ‚‚ renaming (ğ”½ to ğ”¾; ğ’¯ to ğ’¯â‚‚; finiteArity to finArâ‚‚; finiteVars to finVarâ‚‚)

open Signature

open import FreeMonad.Theory
open import FreeMonad.Syntax hiding (OpâŸ¦_âŸ§)
open import FreeMonad.Syntax (ğ”½ âŠ ğ”¾) using (OpâŸ¦_âŸ§)

module _ where
  open SyntaxBind (ğ”½ âŠ ğ”¾)
  commutative : Law â„“ (ğ”½ âŠ ğ”¾)
  commutative .Î“ = Lift (Op ğ”½ Ã— Op ğ”¾)
  commutative .Î½ (lift (fs , gs)) = Lift (Arity ğ”½ fs Ã— Arity ğ”¾ gs)
  commutative .eqn = Î» {
    (lift (fs , gs)) â†’  (do f  â† OpâŸ¦ inl fs  âŸ§ ; g  â† OpâŸ¦ inr gs  âŸ§ ; return (lift (f , g)))
                                              â‰
                        (do g  â† OpâŸ¦ inr gs  âŸ§ ; f  â† OpâŸ¦ inl fs  âŸ§ ; return (lift (f , g)))
    }

open Theory

ğ’¯â€² : Theory _ _
ğ’¯â€² .Laws = âŠ¤
ğ’¯â€² .Eqns _ = commutative

finVarsâ€² : FiniteVars ğ’¯â€²
finVarsâ€² _ (lift (Î³â‚ , Î³â‚‚)) = quotientedChoiceLift (quotientedChoiceProd (finArâ‚ Î³â‚) (finArâ‚‚ Î³â‚‚))

open import FreeMonad.Combinations.Quotient SumTheory.combinedTheory ğ’¯â€² finVarsâ€² public

open import FreeMonad.Quotiented combinedTheory
import FreeMonad.Quotiented SumTheory.combinedTheory as SumQuot

liftâ‚ : Fâ‚.Term A â†’ Term A
liftâ‚ = Fâ‚.interpâ‚œ (opâ‚œ âˆ˜ inl-map) return resp squash/
  where
  resp : (opâ‚œ âˆ˜ inl-map) Models ğ’¯â‚
  resp i t k = 
    let lhs â‰ rhs = Theory.Eqns ğ’¯â‚ i .eqn t
    in interp _ (opâ‚œ âˆ˜ inl-map) k lhs â‰¡âŸ¨ injâ‚-com opâ‚œ k lhs âŸ©
       interp _ opâ‚œ k (injâ‚ lhs) â‰¡âŸ¨ opâ‚œ-resp (inl (inl i)) t k  âŸ©
       interp _ opâ‚œ k (injâ‚ rhs) â‰¡Ë˜âŸ¨ injâ‚-com opâ‚œ k rhs âŸ©
       interp _ (opâ‚œ âˆ˜ inl-map) k rhs âˆ

liftâ‚‚ : Fâ‚‚.Term A â†’ Term A
liftâ‚‚ = Fâ‚‚.interpâ‚œ (opâ‚œ âˆ˜ inr-map) return resp squash/
  where
  resp : (opâ‚œ âˆ˜ inr-map) Models ğ’¯â‚‚
  resp i t k = 
    let lhs â‰ rhs = Theory.Eqns ğ’¯â‚‚ i .eqn t
    in interp _ (opâ‚œ âˆ˜ inr-map) k lhs â‰¡âŸ¨ injâ‚‚-com opâ‚œ k lhs âŸ©
       interp _ opâ‚œ k (injâ‚‚ lhs) â‰¡âŸ¨ opâ‚œ-resp (inl (inr i)) t k  âŸ©
       interp _ opâ‚œ k (injâ‚‚ rhs) â‰¡Ë˜âŸ¨ injâ‚‚-com opâ‚œ k rhs âŸ©
       interp _ (opâ‚œ âˆ˜ inr-map) k rhs âˆ

liftâ‚‚-com : (O : Op ğ”¾) (k : Arity ğ”¾ O â†’ Fâ‚‚.Term B)
          â†’ liftâ‚‚ (Fâ‚‚.opâ‚œ (O , k)) â‰¡ opâ‚œ (inr O , liftâ‚‚ âˆ˜ k)
liftâ‚‚-com O = Fâ‚‚.elim~canonical _ (Î» _ â†’ squash/ _ _) Î» k â†’ cong liftâ‚‚ (Fâ‚‚.opâ‚œ-com-[] k) 

liftâ‚-com : (O : Op ğ”½) (k : Arity ğ”½ O â†’ Fâ‚.Term B)
          â†’ liftâ‚ (Fâ‚.opâ‚œ (O , k)) â‰¡ opâ‚œ (inl O , liftâ‚ âˆ˜ k)
liftâ‚-com O = Fâ‚.elim~canonical _ (Î» _ â†’ squash/ _ _) Î» k â†’ cong liftâ‚ (Fâ‚.opâ‚œ-com-[] k) 

commutesâ€² : (f : Op ğ”½) (g : Op ğ”¾) (k : Arity ğ”½ f â†’ Arity ğ”¾ g â†’ Syntax (ğ”½ âŠ ğ”¾) A)
         â†’ op (inl f , Î» x â†’ op (inr g , Î» y â†’ k x y)) ~â‚œ op (inr g , Î» y â†’ op (inl f , Î» x â†’ k x y))
commutesâ€² f g k = >>=-cong (uncurry k âˆ˜ lower) (eqâ‚œ (inr tt) (lift (f , g)) Syntax.var)

commutes : (f : Op ğ”½) (g : Op ğ”¾) (k : Arity ğ”½ f â†’ Arity ğ”¾ g â†’ Term A)
         â†’ (do x â† Opâ‚œâŸ¦ inl f âŸ§ ; y â† Opâ‚œâŸ¦ inr g âŸ§ ; k x y) â‰¡ (do y â† Opâ‚œâŸ¦ inr g âŸ§ ; x â† Opâ‚œâŸ¦ inl f âŸ§ ; k x y)
commutes f g k = cong (_>>= (uncurry k âˆ˜ lower)) (eq/ _ _ (eqâ‚œ (inr tt) (lift (f , g)) Syntax.var))

commutes-multâ‚ : (fs : Op ğ”½) (ys : Fâ‚‚.Term B) (k : Arity ğ”½ fs â†’ B â†’ Term C)
               â†’ (do f â† [ op (inl fs , Syntax.var) ] ; y â† liftâ‚‚ ys ; k f y) â‰¡ (do y â† liftâ‚‚ ys ; f â† [ op (inl fs , Syntax.var) ] ; k f y)
commutes-multâ‚ fs ys k =
  elimProp/
    {P = Î» ys â†’ (do f â† [ op (inl fs , Syntax.var) ] ; y â† liftâ‚‚ ys ; k f y) â‰¡ (do y â† liftâ‚‚ ys ; f â† [ op (inl fs , Syntax.var) ] ; k f y)}
    (Î» _ â†’ squash/ _ _)
    (elim-s _ _ (Î» { (var x) â†’ refl
                   ; (op (Oy , ys) PâŸ¨ysâŸ©) â†’

                     (do f â† [ op (inl fs , Syntax.var) ] ; y â† liftâ‚‚ [ op (Oy , ys) ] ; k f y) â‰¡âŸ¨ f â‡ [ op (inl fs , Syntax.var) ] â¨¾/ assoc [ op (inr Oy , Syntax.var) ] _ _ âŸ©
                     (do f â† [ op (inl fs , Syntax.var) ] ; yâ€² â† [ op (inr Oy , Syntax.var) ] ; y â† liftâ‚‚ [ ys yâ€² ] ; k f y) â‰¡âŸ¨ commutes fs Oy _ âŸ©
                     (do yâ€² â† [ op (inr Oy , Syntax.var) ] ; f â† [ op (inl fs , Syntax.var) ] ; y â† liftâ‚‚ [ ys yâ€² ] ; k f y) â‰¡âŸ¨ yâ€² â‡ [ op (inr Oy , Syntax.var) ] â¨¾/ PâŸ¨ysâŸ© yâ€² âŸ©
                     (do yâ€² â† [ op (inr Oy , Syntax.var) ] ; y â† liftâ‚‚ [ ys yâ€² ] ; f â† [ op (inl fs , Syntax.var) ] ; k f y) â‰¡Ë˜âŸ¨ assoc [ op (inr Oy , Syntax.var) ] _ _ âŸ©
                     (do y â† ([ op (inr Oy , Syntax.var) ] >>= Î» yâ€² â†’ liftâ‚‚ [ ys yâ€² ]) ; f â† [ op (inl fs , Syntax.var) ] ; k f y) â‰¡âŸ¨âŸ©
                     (do y â† opâ‚œ (inr Oy , Î» yâ€² â†’ liftâ‚‚ [ ys yâ€² ]) ; f â† [ op (inl fs , Syntax.var) ] ; k f y) â‰¡âŸ¨âŸ©
                     (do y â† liftâ‚‚ [ op (Oy , ys) ] ; f â† [ op (inl fs , Syntax.var) ] ; k f y) âˆ

                   }))
    ys

commutes-mult : (xs : Fâ‚.Term A) (ys : Fâ‚‚.Term B) (k : A â†’ B â†’ Term C)
              â†’ (do x â† liftâ‚ xs ; y â† liftâ‚‚ ys ; k x y) â‰¡ (do y â† liftâ‚‚ ys ; x â† liftâ‚ xs ; k x y)
commutes-mult xs ys k =
  elimProp/
    {P = Î» xs â†’ (do x â† liftâ‚ xs ; y â† liftâ‚‚ ys ; k x y) â‰¡ (do y â† liftâ‚‚ ys ; x â† liftâ‚ xs ; k x y)}
    (Î» _ â†’ squash/ _ _)
    (elim-s _ _ Î» { (var x) â†’ refl ; (op (Ox , xs) PâŸ¨xsâŸ©) â†’

    (do x â† liftâ‚ [ op (Ox , xs) ]
        y â† liftâ‚‚ ys
        k x y) â‰¡âŸ¨ assoc [ op (inl Ox , Syntax.var) ] _ _ âŸ©

    (do i â† [ op (inl Ox , Syntax.var) ]
        x â† liftâ‚ [ xs i ]
        y â† liftâ‚‚ ys
        k x y) â‰¡âŸ¨ i â‡ [ op (inl Ox , Syntax.var) ] â¨¾/ PâŸ¨xsâŸ© i âŸ©

    (do i â† [ op (inl Ox , Syntax.var) ]
        y â† liftâ‚‚ ys
        x â† liftâ‚ [ xs i ]
        k x y) â‰¡âŸ¨ commutes-multâ‚ Ox ys _ âŸ©

    (do y â† liftâ‚‚ ys
        i â† [ op (inl Ox , Syntax.var) ]
        x â† liftâ‚ [ xs i ]
        k x y) â‰¡Ë˜âŸ¨ y â‡ liftâ‚‚ ys â¨¾/ assoc [ op (inl Ox , Syntax.var) ] _ _ âŸ©

    (do y â† liftâ‚‚ ys
        x â† liftâ‚ [ op (Ox , xs) ]
        k x y) âˆ

    })
    xs

âŠ—-resp : (Ï• : ğ”½ -Alg[ A ]) (Ïˆ : ğ”¾ -Alg[ A ])
       â†’ (âˆ€ t â†’ (Ï• -âŠ- Ïˆ) Respects (commutative .eqn t))
       â†’ Ï• Models ğ’¯â‚
       â†’ Ïˆ Models ğ’¯â‚‚
       â†’ (Ï• -âŠ- Ïˆ) Models ğ’¯
âŠ—-resp Ï• Ïˆ respâŠ respâ‚ respâ‚‚ (inr tt) t k = respâŠ t k
âŠ—-resp Ï• Ïˆ respâŠ respâ‚ respâ‚‚ (inl (inl i)) t k = let lhs â‰ rhs = Theory.Eqns ğ’¯â‚ i .eqn t in interpâ‚ Ï• Ïˆ k lhs â¨¾ respâ‚ i t k â¨¾ sym (interpâ‚ Ï• Ïˆ k rhs)
âŠ—-resp Ï• Ïˆ respâŠ respâ‚ respâ‚‚ (inl (inr i)) t k = let lhs â‰ rhs = Theory.Eqns ğ’¯â‚‚ i .eqn t in interpâ‚‚ Ï• Ïˆ k lhs â¨¾ respâ‚‚ i t k â¨¾ sym (interpâ‚‚ Ï• Ïˆ k rhs)
