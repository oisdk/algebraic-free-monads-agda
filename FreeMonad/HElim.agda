{-# OPTIONS --safe #-}

open import Prelude
open import FreeMonad.PackagedTheory
open import Signatures
import FreeMonad.Theory
open import Finite

module FreeMonad.HElim
  (T : FullTheory â„“zero)
  (findVar : FreeMonad.Theory.DiscreteVars (FullTheory.ğ’¯ T))
  (fin-arities : âˆ€ Oáµ¢ â†’ â„° (Signature.Arity (FullTheory.ğ”½ T) Oáµ¢))
  where

open FullTheory T

open module ğ•‹ = FreeMonad.Theory â„“zero ğ”½

open import FreeMonad.Syntax ğ”½
open SyntaxBind using (_<$>_; <$>-comp) renaming (_>>=_ to _>>=â‚›_)
open import FreeMonad.Syntax.AsSignature ğ”½ hiding (_âˆˆ_ ; _âˆ‰_)
open import FreeMonad.Relation ğ”½ ğ’¯
open import Truth
open import Data.Sigma.Properties
open import Data.Lift.Properties

open Theory ğ’¯

open Signature ğ”½ using (Arity)

disc-arities : âˆ€ Oáµ¢ â†’ Discrete (Arity Oáµ¢)
disc-arities Oáµ¢ = â„°â‡’Discrete (fin-arities Oáµ¢)

setArity : âˆ€ Oáµ¢ â†’ isSet (Arity Oáµ¢)
setArity O = Discreteâ†’isSet (disc-arities O)


infixr 5 _âˆˆ_ _âˆ‰_
_âˆˆ_ _âˆ‰_ : A â†’ (B â†’ A) â†’ Type _
_âˆˆ_ = flip Fibre
x âˆ‰ xs = Â¬ (x âˆˆ xs)

â„°âŸ¨IndexâŸ© : (s : Syntax A) â†’ â„° (Index s)
â„°âŸ¨IndexâŸ© = elim-s _ Î» { (var x) â†’ â„°âŸ¨âŠ¤âŸ© ; (op (O , k) PâŸ¨xsâŸ©) â†’ â„°Î£ (fin-arities O) PâŸ¨xsâŸ© }

âˆˆ? : Discrete A â†’ (x : A) (s : Syntax B) (xs : Index s â†’ A) â†’ Dec âˆ¥ x âˆˆ xs âˆ¥
âˆˆ? discrete x s xs = â„°â‡’search (â„°âŸ¨IndexâŸ© s) Î» i â†’ discrete (xs i) x


record Provenance {A : Type a} (s t : Syntax A) : Type (â„“suc a) where
  field
    Iâ‚š   : Type a
    Iâ‰Ÿ   : Discrete Iâ‚š
    sâ‚š   : Index s â†’ Iâ‚š
    tâ‚š   : Index t â†’ Iâ‚š
    eqvâ‚š : fill _ sâ‚š ~â‚œ fill _ tâ‚š
    kâ‚š   : Iâ‚š â†’ A
    s-k  : âˆ€ i â†’ lookup s i â‰¡ kâ‚š (sâ‚š i)
    t-k  : âˆ€ i â†’ lookup t i â‰¡ kâ‚š (tâ‚š i)

  âˆˆs? : âˆ€ v â†’ Dec âˆ¥ v âˆˆ sâ‚š âˆ¥
  âˆˆs? v = âˆˆ? Iâ‰Ÿ v s sâ‚š

  âˆˆt? : âˆ€ v â†’ Dec âˆ¥ v âˆˆ tâ‚š âˆ¥
  âˆˆt? v = âˆˆ? Iâ‰Ÿ v t tâ‚š


module DeriveEq {A : Type a} (i : Laws) (Î“ : Eqns i .Î“) (k : Eqns i .Î½ Î“ â†’ Syntax A) where
  open Equation (eqn (Eqns i) Î“) renaming (lhs to lhsâˆ™ ; rhs to rhsâˆ™) using ()


  private module DisplayI where

    Iâ‚š : Type
    Iâ‚š = Î£[ v â¦‚ Eqns i .Î½ Î“ ] Ã— Index (k v)

  Iâ‚š : Type a
  Iâ‚š = Lift (Î£[ v â¦‚ Eqns i .Î½ Î“ ] Ã— Index (k v))

  Iâ‰Ÿ : Discrete Iâ‚š
  Iâ‰Ÿ = discreteLift (discreteÎ£ (findVar i Î“) Î» x â†’ disc-index disc-arities (k x))

  kâ‚š : Iâ‚š â†’ A
  kâ‚š = uncurry (lookup âˆ˜â€² k) âˆ˜ lower

  sâ‚š : >>=-ind lhsâˆ™ k â†’ Iâ‚š
  sâ‚š = lift âˆ˜ map-Î£ (lookup lhsâˆ™) id âˆ˜â€² pull-shape k lhsâˆ™

  tâ‚š : >>=-ind rhsâˆ™ k â†’ Iâ‚š
  tâ‚š = lift âˆ˜ map-Î£ (lookup rhsâˆ™) id âˆ˜â€² pull-shape k rhsâˆ™ 

  eqvâ‚š : fill _ sâ‚š ~â‚œ fill _ tâ‚š
  eqvâ‚š = reflâ‚œ (>>=âŸ¦âŸ§ k _ lhsâˆ™)
    âŸ¨ transâ‚œ âŸ© eqâ‚œ i Î“ (Î» v â†’ fill _ (lift âˆ˜ (v ,_)))
    âŸ¨ transâ‚œ âŸ© reflâ‚œ (sym (>>=âŸ¦âŸ§ k _ rhsâˆ™))

  s-k : âˆ€ i â†’ lookup (lhsâˆ™ >>=â‚› k) i â‰¡ kâ‚š (sâ‚š i)
  s-k i = sym (lookup-pull k lhsâˆ™ i)

  t-k : âˆ€ i â†’ lookup (rhsâˆ™ >>=â‚› k) i â‰¡ kâ‚š (tâ‚š i)
  t-k i = sym (lookup-pull k rhsâˆ™ i)


module DeriveCong {A : Type a} Oáµ¢ (kâ‚— káµ£ : Arity Oáµ¢ â†’ Syntax A) (derive-at : âˆ€ i â†’ Provenance (kâ‚— i) (káµ£ i)) where
  module _ (i : Arity Oáµ¢) where module ğ’Ÿ = Provenance (derive-at i)

  s t : Syntax A
  s = op (Oáµ¢ , kâ‚—)
  t = op (Oáµ¢ , káµ£)

  Iâ‚š : Type _
  Iâ‚š = Î£[ i â¦‚ Arity Oáµ¢ ] Ã— ğ’Ÿ.Iâ‚š i

  Iâ‰Ÿ : Discrete Iâ‚š
  Iâ‰Ÿ = discreteÎ£ (disc-arities _) ğ’Ÿ.Iâ‰Ÿ

  sâ‚š : Index s â†’ Iâ‚š
  sâ‚š (i , is) = i , ğ’Ÿ.sâ‚š i is

  tâ‚š : Index t â†’ Iâ‚š
  tâ‚š (i , is) = i , ğ’Ÿ.tâ‚š i is

  eqvâ‚š : fill _ sâ‚š ~â‚œ fill _ tâ‚š
  eqvâ‚š = congâ‚œ Oáµ¢ _ _ Î» i â†’
              reflâ‚œ (sym (<$>-com-inv (_,_ i) (_ , ğ’Ÿ.sâ‚š i)))
    âŸ¨ transâ‚œ âŸ© <$>-cong (_,_ i) (ğ’Ÿ.eqvâ‚š i)
    âŸ¨ transâ‚œ âŸ© reflâ‚œ (<$>-com-inv (_,_ i) (_ , ğ’Ÿ.tâ‚š i))

  kâ‚š : Iâ‚š â†’ A
  kâ‚š = uncurry ğ’Ÿ.kâ‚š

  s-k : âˆ€ i â†’ lookup s i â‰¡ kâ‚š (sâ‚š i)
  s-k = uncurry ğ’Ÿ.s-k

  t-k : âˆ€ i â†’ lookup t i â‰¡ kâ‚š (tâ‚š i)
  t-k = uncurry ğ’Ÿ.t-k


module DeriveTrans {A : Type a} (setA : isSet A) (s t u : Syntax A) (lhsâ€² : Provenance s u) (rhsâ€² : Provenance u t) where
  module rhs = Provenance rhsâ€²
  module lhs = Provenance lhsâ€²

  open import HITs.Pushout
  open import HITs.Pushout.Properties using (fin-disc-pushout)

  Iâ‚š : Type a
  Iâ‚š = Pushout {A = Index u} {B = lhs.Iâ‚š} {C = rhs.Iâ‚š} lhs.tâ‚š rhs.sâ‚š

  Iâ‰Ÿ : Discrete Iâ‚š
  Iâ‰Ÿ = fin-disc-pushout (â„°âŸ¨IndexâŸ© u) lhs.Iâ‰Ÿ rhs.Iâ‰Ÿ

  kâ‚š : Iâ‚š â†’ A
  kâ‚š (inl x)  = lhs.kâ‚š x
  kâ‚š (inr x)  = rhs.kâ‚š x
  kâ‚š (push i j) = (sym (lhs.t-k i) â¨¾ rhs.s-k i) j
  kâ‚š (trunc x y p q i j) =
    setA (kâ‚š x) (kâ‚š y) (cong kâ‚š p) (cong kâ‚š q) i j

  sâ‚š : Index s â†’ Iâ‚š
  sâ‚š = inl âˆ˜ lhs.sâ‚š
  tâ‚š : Index t â†’ Iâ‚š
  tâ‚š = inr âˆ˜ rhs.tâ‚š

  eqvâ‚š : fill _ sâ‚š ~â‚œ fill _ tâ‚š
  eqvâ‚š =       substâ‚‚ _~â‚œ_ (<$>-com-inv inl (_ , lhs.sâ‚š)) (<$>-com-inv inl (_ , lhs.tâ‚š)) (<$>-cong inl lhs.eqvâ‚š)
    âŸ¨ transâ‚œ âŸ© reflâ‚œ (cong (fill (shape u)) (funExt push))
    âŸ¨ transâ‚œ âŸ© substâ‚‚ _~â‚œ_ (<$>-com-inv inr (_ , rhs.sâ‚š)) (<$>-com-inv inr (_ , rhs.tâ‚š)) (<$>-cong inr rhs.eqvâ‚š)

  s-k = lhs.s-k
  t-k = rhs.t-k

module DeriveRefl {A : Type a} (s : Syntax A) where
  Iâ‚š : Type a
  Iâ‚š = Lift (Index s)

  Iâ‰Ÿ : Discrete Iâ‚š
  Iâ‰Ÿ = discreteLift (disc-index disc-arities s)

  sâ‚š tâ‚š : Index s â†’ Iâ‚š
  sâ‚š = lift
  tâ‚š = lift

  eqvâ‚š : fill _ sâ‚š ~â‚œ fill _ tâ‚š
  eqvâ‚š = reflâ‚œ refl

  kâ‚š : Iâ‚š â†’ A
  kâ‚š = lookup s âˆ˜ lower

  s-k t-k : âˆ€ i â†’ lookup s i â‰¡ kâ‚š (lift i)
  s-k _ = refl
  t-k _ = refl


module DeriveSym {A : Type a} (s t : Syntax A) (derived-rev : Provenance t s) where
  module ğ“ˆ = Provenance derived-rev

  Iâ‚š = ğ“ˆ.Iâ‚š
  Iâ‰Ÿ = ğ“ˆ.Iâ‰Ÿ
  sâ‚š = ğ“ˆ.tâ‚š
  tâ‚š = ğ“ˆ.sâ‚š
  eqvâ‚š = symâ‚œ ğ“ˆ.eqvâ‚š
  kâ‚š  = ğ“ˆ.kâ‚š
  s-k = ğ“ˆ.t-k
  t-k = ğ“ˆ.s-k


module _ (setA : isSet A) where
  open Theory ğ’¯
  open import FinitenessConditions using (SplitQuotientedChoiceâ‡’Choice)

  derive : (s t : Syntax A) â†’ s ~â‚œ t â†’ âˆ¥ Provenance s t âˆ¥
  derive _ _ (eqâ‚œ i Î“ k)        = âˆ£ record { DeriveEq i Î“ k } âˆ£
  derive s t (reflâ‚œ p)          = âˆ£ subst (Provenance s) p (record { DeriveRefl s }) âˆ£
  derive s t (symâ‚œ p)           = âˆ¥mapâˆ¥ (Î» pâ€² â†’ record { DeriveSym s t pâ€² }) (derive _ _ p)
  derive s t (transâ‚œ p q)       = âˆ¥liftA2âˆ¥ (Î» lhs rhs â†’ record { DeriveTrans setA s t _ lhs rhs }) (derive _ _ p) (derive _ _ q)
  derive s t (truncâ‚œ p q i)     = squash (derive s t p) (derive s t q) i
  derive _ _ (congâ‚œ Oáµ¢ kâ‚— káµ£ x) =
    âˆ¥mapâˆ¥ (Î» d â†’ record { DeriveCong Oáµ¢ kâ‚— káµ£ d })
          (SplitQuotientedChoiceâ‡’Choice (setArity Oáµ¢) (finiteArity Oáµ¢) Î» i â†’ derive (kâ‚— i) (káµ£ i) (x i))

private
  variable
    s : Syntax A
    t : Syntax B


import FreeMonad.Quotiented T as OnTerm

open OnTerm
  using (syntactic-bind; join; >>=-join-eq; >>=-join-comm; <$>-comm)
  renaming (_<$>_ to _<$>â‚œ_ ; _>>=_ to _>>=â‚œ_ ; return to returnâ‚œ)

module _ {A : Type a} (Ï• : A â†’ Î© c)
  where
  Ï•? : A â†’ A Ã— Î© c
  Ï•? x = x , Ï• x

  Ï•T : A â†’ A Ã— Î© c
  Ï•T x = x , True

  module _ (setA : isSet A) where

    -- This can be cleaned up a lot: since we switched to the container rep, a lot
    -- of the proofs here should trivialise if stated carefully.
    module _ (setB : isSet B)
            (f g : A â†’ B) (k : (x : A) â†’ ProofOf (Ï• x) â†’ f x â‰¡ g x)
            (s : Syntax A)          
            (pr : Ï•? <$> s ~â‚œ Ï•T <$> s)
            where
      ğ’¢-elimâ€² : f <$> s ~â‚œ g <$> s

      ğ’¢-elimâ€² = âˆ¥recâˆ¥ truncâ‚œ Helper.theorem (derive (isSetÎ£ setA (Î» _ â†’ isSetÎ©)) (Ï•? <$> s) (Ï•T <$> s) pr)
        where
        module Helper
          (d : Provenance (Ï•? <$> s) (Ï•T <$> s)) where
          open Provenance d

          kâ€² : Iâ‚š â†’ B
          kâ€² i =  let v = kâ‚š i .fst in if  does (âˆˆs? i) then f v else g v
          âˆˆsâ‚šâ‡’f : (i : Index (Ï•? <$> s)) â†’ kâ€² (sâ‚š i) â‰¡ f (fst (kâ‚š (sâ‚š i)))
          âˆˆsâ‚šâ‡’f i with âˆˆs? (sâ‚š i)
          ... | yes _ = refl
          ... | no Â¬p = absurd (Â¬p âˆ£ i , refl âˆ£)

          tsâ‚ : f <$> s â‰¡ kâ€² <$> fill _ sâ‚š
          tsâ‚ =
            f <$> s                                â‰¡âŸ¨ <$>-comp s _ _ âŸ©
            f âˆ˜ fst <$> Ï•? <$> s                   â‰¡Ë˜âŸ¨ cong (f âˆ˜ fst <$>_) (Indexed .leftInv (Ï•? <$> s)) âŸ©
            f âˆ˜ fst <$> fill _ (lookup (Ï•? <$> s)) â‰¡âŸ¨ cong ((f âˆ˜ fst <$>_) âˆ˜ fill _) (funExt s-k) âŸ©
            f âˆ˜ fst <$> fill _ (kâ‚š âˆ˜ sâ‚š)           â‰¡âŸ¨ <$>-com-inv (f âˆ˜ fst) _ âŸ©
            fill _ (f âˆ˜ fst âˆ˜ kâ‚š âˆ˜ sâ‚š)             â‰¡Ë˜âŸ¨ cong (fill _) (funExt âˆˆsâ‚šâ‡’f) âŸ©
            fill _ (kâ€² âˆ˜ sâ‚š)                       â‰¡Ë˜âŸ¨ <$>-com-inv kâ€² _ âŸ©
            kâ€² <$> fill _ sâ‚š âˆ
          âˆˆtâ‚šâ‡’g : (i : Index (Ï•T <$> s)) â†’ kâ€² (tâ‚š i) â‰¡ g (fst (kâ‚š (tâ‚š i)))
          âˆˆtâ‚šâ‡’g i with âˆˆs? (tâ‚š i)
          ... | no _ = refl
          ... | yes j,q = flip (âˆ¥recâˆ¥ (setB _ _)) j,q (Î» { (j , q) â†’
              k (kâ‚š (tâ‚š i) .fst) $ extract $
                Ï• (kâ‚š (tâ‚š i) .fst)            â‰¡Ë˜âŸ¨ cong (Ï• âˆ˜ fst) (s-k j â¨¾ cong kâ‚š q) âŸ©
                Ï• (lookup (Ï•? <$> s)  j .fst) â‰¡âŸ¨ cong (Ï• âˆ˜ fst) (<$>-lookup Ï•? s j) â¨¾ cong snd (sym (<$>-lookup Ï•? s j)) âŸ© 
                lookup (Ï•? <$> s) j .snd      â‰¡âŸ¨ cong snd (s-k j â¨¾ cong kâ‚š q â¨¾ sym (t-k i)) âŸ©
                lookup (Ï•T <$> s) i .snd      â‰¡âŸ¨ cong snd (<$>-lookup Ï•T s i) âŸ©
                True âˆ
              })

          tsâ‚‚ : g <$> s â‰¡ kâ€² <$> fill _ tâ‚š
          tsâ‚‚ = 
            g <$> s                                â‰¡âŸ¨ <$>-comp s _ _ âŸ©
            g âˆ˜ fst <$> Ï•T <$> s                   â‰¡Ë˜âŸ¨ cong (g âˆ˜ fst <$>_) (Indexed .leftInv (Ï•T <$> s)) âŸ©
            g âˆ˜ fst <$> fill _ (lookup (Ï•T <$> s)) â‰¡âŸ¨ cong ((g âˆ˜ fst <$>_) âˆ˜ fill _) (funExt t-k) âŸ©
            g âˆ˜ fst <$> fill _ (kâ‚š âˆ˜ tâ‚š)           â‰¡âŸ¨ <$>-com-inv (g âˆ˜ fst) _ âŸ©
            fill _ (g âˆ˜ fst âˆ˜ kâ‚š âˆ˜ tâ‚š)             â‰¡Ë˜âŸ¨ cong (fill _) (funExt âˆˆtâ‚šâ‡’g)  âŸ©
            fill _ (kâ€² âˆ˜ tâ‚š)                       â‰¡Ë˜âŸ¨ <$>-com-inv kâ€² _ âŸ©
            kâ€² <$> fill _ tâ‚š âˆ

          theorem : f <$> s ~â‚œ g <$> s
          theorem = reflâ‚œ tsâ‚ âŸ¨ transâ‚œ âŸ© <$>-cong kâ€² eqvâ‚š âŸ¨ transâ‚œ âŸ© reflâ‚œ (sym tsâ‚‚)

    module _ {B : Type b}
            (f g : A â†’ Term B) (k : (x : A) â†’ ProofOf (Ï• x) â†’ f x â‰¡ g x) (p : Term A)
            (path : Ï•? <$>â‚œ p â‰¡ Ï•T <$>â‚œ p)
            where
      ğ’¢-elimâ€³ : p >>=â‚œ f â‰¡ p >>=â‚œ g
      ğ’¢-elimâ€³ = elimProp/-with {D = Î» p â†’ (Ï•? <$>â‚œ p) â‰¡ (Ï•T <$>â‚œ p)} (Î» p _ â†’ squash/ (p >>=â‚œ f) (p >>=â‚œ g)) go p path
        where
        module _ (pâ‚› : Syntax A) (path : Ï•? <$>â‚œ [ pâ‚› ] â‰¡ Ï•T <$>â‚œ [ pâ‚› ]) where
          d : Ï•? <$> pâ‚› ~â‚œ Ï•T <$> pâ‚›
          d = ~â‚œ-effective (Ï•? <$>  pâ‚›) (Ï•T <$> pâ‚›) (sym (<$>-comm pâ‚› Ï•?) â¨¾ path â¨¾ <$>-comm pâ‚› Ï•T)

          h : ([ f <$> pâ‚› ] â¦‚ Term _) â‰¡ [ g <$> pâ‚› ]
          h = eq/ _ _ (ğ’¢-elimâ€² squash/ f g k pâ‚› d)

          go : [ pâ‚› ] >>=â‚œ f â‰¡ [ pâ‚› ] >>=â‚œ g
          go =
            [ pâ‚› ] >>=â‚œ f        â‰¡âŸ¨ >>=-join-comm pâ‚› f âŸ©
            [ f <$> pâ‚› ] >>=â‚œ id â‰¡âŸ¨ cong (_>>=â‚œ id) h âŸ©
            [ g <$> pâ‚› ] >>=â‚œ id â‰¡Ë˜âŸ¨ >>=-join-comm pâ‚› g âŸ©
            [ pâ‚› ] >>=â‚œ g âˆ

ğ’¢-elim  :  (Ï• : A â†’ Î© c)
        â†’  (f g : A â†’ Term B)
        â†’  (k : (x : A) â†’ ProofOf (Ï• x) â†’ f x â‰¡ g x)
        â†’  (p : Term A)
        â†’  (Ï•? Ï• <$>â‚œ p â‰¡ Ï•T Ï• <$>â‚œ p)
        â†’  (p >>=â‚œ f) â‰¡ (p >>=â‚œ g)
ğ’¢-elim Ï• f g k p path =
  let h = ğ’¢-elimâ€³
             (âˆ¥recâˆ¥â‚‚ isSetÎ© Ï•)
             squashâ‚‚
             (âˆ¥recâˆ¥â‚‚ squash/ f)
             (âˆ¥recâˆ¥â‚‚ squash/ g)
             (âˆ¥elimâˆ¥â‚‚ (Î» _ â†’ isSetâ†’ (isPropâ†’isSet (squash/ _ _))) k)
             (âˆ£_âˆ£â‚‚ <$>â‚œ p)
             ( OnTerm.<$>-comp _ _ p
             â¨¾ sym (OnTerm.<$>-comp (mapâ‚ âˆ£_âˆ£â‚‚) (Ï•? Ï•) p)
             â¨¾ cong (mapâ‚ âˆ£_âˆ£â‚‚ <$>â‚œ_) path
             â¨¾ OnTerm.<$>-comp (mapâ‚ âˆ£_âˆ£â‚‚) (Ï•T Ï•) p
             â¨¾ sym (OnTerm.<$>-comp _ _ p))
  in sym (OnTerm.assoc p (OnTerm.return âˆ˜ âˆ£_âˆ£â‚‚) (âˆ¥recâˆ¥â‚‚ squash/ f)) â¨¾ h â¨¾ OnTerm.assoc p (OnTerm.return âˆ˜ âˆ£_âˆ£â‚‚) (âˆ¥recâˆ¥â‚‚ squash/ g)
