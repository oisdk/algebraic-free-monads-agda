
{-# OPTIONS --safe #-}

open import Signatures
open import Prelude hiding (Iso)

module FreeMonad.Syntax (ğ”½ : Signature) where

open Signature ğ”½

--------------------------------------------------------------------------------
-- Unquotiented Free Monad
--------------------------------------------------------------------------------

data Syntax (Î½ : Type a) : Type a where
  var  : Î½                 â†’ Syntax Î½
  op   : âŸ¦ ğ”½ âŸ§ (Syntax Î½)  â†’ Syntax Î½

OpâŸ¦_âŸ§ : âˆ€ o â†’ Syntax (Arity o)
OpâŸ¦ o âŸ§ = op (o , var)

private variable Î½ : Type a

syntax-alg : ğ”½ -Alg[ Syntax Î½ ]
syntax-alg = op
syntax-alg' : (v : Type) â†’ ğ”½ -Alg
syntax-alg' Î½ = Syntax Î½ , op

--------------------------------------------------------------------------------
-- Fold
--------------------------------------------------------------------------------

private variable ğ’ : Type c
interp : ğ”½ -Alg[ ğ’ ] â†’ (Î½ â†’ ğ’) â†’ Syntax Î½ â†’ ğ’
interp Ï• Ï (var x)        = Ï x
interp Ï• Ï (op (Oáµ¢ , k))  = Ï• (Oáµ¢ , Î» i â†’ interp Ï• Ï (k i))

--------------------------------------------------------------------------------
-- The base *dependent* functor for this type:
--------------------------------------------------------------------------------

data ğ”–ğ”¶ğ”«ğ”±ğ”ğ”µ (A : Type a) (B : Type b) (P : B â†’ Type c) : Type (a â„“âŠ” b â„“âŠ” c) where
  var  :  A                              â†’ ğ”–ğ”¶ğ”«ğ”±ğ”ğ”µ A B P
  op   :  (xs : âŸ¦ ğ”½ âŸ§ B)
          (PâŸ¨xsâŸ© : âˆ€ i â†’ P (xs .snd i))  â†’ ğ”–ğ”¶ğ”«ğ”±ğ”ğ”µ A B P

--------------------------------------------------------------------------------
-- Dependent eliminators for the syntax type
--------------------------------------------------------------------------------

âŸª_âŸ« : ğ”–ğ”¶ğ”«ğ”±ğ”ğ”µ A (Syntax A) P â†’ Syntax A
âŸª var x    âŸ« = var x
âŸª op xs _  âŸ« = op xs

DepAlg : (A : Type a) â†’ (Syntax A â†’ Type b) â†’ Type (a â„“âŠ” b)
DepAlg A P = (xs : ğ”–ğ”¶ğ”«ğ”±ğ”ğ”µ A (Syntax A) P) â†’ P âŸª xs âŸ«

syntax DepAlg A (Î» xs â†’ P) = Î¨[ xs â¦‚ Syntax A ] P

elim-s : (P : Syntax A â†’ Type b) â†’ DepAlg A P â†’ (xs : Syntax A) â†’ P xs
elim-s _ alg (var x)         = alg (var x)
elim-s P alg (op (Op , xs))  = alg (op (Op , xs) (Î» i â†’ elim-s P alg (xs i)))

--------------------------------------------------------------------------------
-- Bind
--------------------------------------------------------------------------------

module SyntaxBind where
  return : A â†’ Syntax A
  return = var

  private variable
    Î½â€² : Type b

  infixl 4.5 _=<<_
  _=<<_ : (Î½ â†’ Syntax Î½â€²) â†’ Syntax Î½ â†’ Syntax Î½â€²
  _=<<_ = interp op

  infixr 4.5 _>>=_
  _>>=_ : Syntax A â†’ (A â†’ Syntax B) â†’ Syntax B
  xs >>= Ï = interp op Ï xs

  infixr 4.5 _>>_
  _>>_ : Syntax A â†’ Syntax B â†’ Syntax B
  xs >> ys = xs >>= Î» _ â†’ ys

  infixr 9 _<=<_
  _<=<_ : (B â†’ Syntax C) â†’ (A â†’ Syntax B) â†’ A â†’ Syntax C
  (f <=< g) x = f =<< g x

  infixl 9 _>=>_
  _>=>_ : (A â†’ Syntax B) â†’ (B â†’ Syntax C) â†’ A â†’ Syntax C
  _>=>_ = flip _<=<_

  infixr 4.5 _<$>_
  _<$>_ : (A â†’ B) â†’ Syntax A â†’ Syntax B
  f <$> xs = xs >>= var âˆ˜ f

  infixl 4.5 _<&>_
  _<&>_ : Syntax A â†’ (A â†’ B) â†’ Syntax B
  xs <&> f = f <$> xs

  <$>-comp : (xs : Syntax A) â†’ (f : A â†’ B) â†’ (g : B â†’ C) â†’ ((g âˆ˜ f) <$> xs) â‰¡ (g <$> (f <$> xs))
  <$>-comp (var x)      f g   = refl
  <$>-comp (op (o , k)) f g i = op (o , Î» a â†’ <$>-comp (k a) f g i)

open SyntaxBind

--------------------------------------------------------------------------------
-- Properties about interps on syntax
--------------------------------------------------------------------------------

interp-id : 
  (xs : Syntax Î½) â†’ interp op var xs â‰¡ xs
interp-id (var x)        = refl
interp-id (op (Oáµ¢ , k))  = cong (op âˆ˜ _,_ Oáµ¢) (funExt Î» i â†’ interp-id (k i))

interp-fusion  :  (f : C â†’ A) {gâ‚ : âŸ¦ ğ”½ âŸ§ C â†’ C} {gâ‚‚ : âŸ¦ ğ”½ âŸ§ A â†’ A} (c : B â†’ C) â†’
                  (âˆ€ xs â†’ f (gâ‚ xs) â‰¡ gâ‚‚ (cmap f xs)) â†’ 
               âˆ€  xs â†’ f (interp gâ‚ c xs) â‰¡ interp gâ‚‚ (f âˆ˜ c) xs
interp-fusion f            c p (var x)          = refl
interp-fusion f {gâ‚‚ = gâ‚‚}  c p (op (Oáµ¢ , xs) )  =
  p _ â¨¾ cong gâ‚‚ (cong (Oáµ¢ ,_) (funExt Î» x â†’ interp-fusion f c p (xs x)))

interp-comp : âˆ€ (f : âŸ¦ ğ”½ âŸ§ A â†’ A) (k : B â†’ A) (c : C â†’ Syntax B) xs â†’
              interp f k (xs >>= c) â‰¡ interp f (interp f k âˆ˜ c) xs
interp-comp f k c = interp-fusion (interp f k) c Î» _ â†’ refl

-- interp is a homomorphism
interp-homo : âˆ€ {Î½ C : Type} â†’ (alg : ğ”½ -Alg[ C ]) â†’ (Î½ â†’ C)
            â†’ ((Syntax Î½ , syntax-alg) âŸ¶ (C , alg))
interp-homo {Î½ = Î½} alg k = interp alg k , funExt comm
  where
    comm :  (x : âŸ¦ ğ”½ âŸ§ (Syntax Î½)) â†’ interp alg k (op x) â‰¡ alg (cmap (interp alg k) x)
    comm (o , xs) = refl

--------------------------------------------------------------------------------
-- Freeness of Syntax
--------------------------------------------------------------------------------

var-alg : (Î½ : Type) â†’ (C : Type) â†’ (alg : ğ”½ -Alg[ C ])
        â†’ ((Syntax Î½ , syntax-alg {Î½ = Î½}) âŸ¶ (C , alg)) â†’ Î½ â†’ C
var-alg Î½ C alg (h , _) x = h (var x)

open import Cubical.Foundations.Isomorphism using (isoToIsEquiv; Iso)

-- The homotopy freeness is essentially Theorem 5.4.7 in the HoTT book and studied
-- in detail in the following paper:
--
--   Steve Awodey, Nicola Gambino, and Kristina Sojakova. 2017. Homotopy-Initial
--   Algebras in Type Theory. J. ACM 63, 6, Article 51 (February 2017), 45 pages.
--   https://doi.org/10.1145/3006383
--
-- The following proof is generalised from the proof of homotopy initiality of natural
-- numbers in the cubical library (https://agda.github.io/cubical/Cubical.Data.Nat.Algebra.html).

syntax-is-free : âˆ€ {Î½ : Type} â†’ (C : Type) â†’ (alg : ğ”½ -Alg[ C ])
               â†’ isEquiv (var-alg Î½ C alg)
syntax-is-free {Î½} C alg = isoToIsEquiv i where
  i : Iso ((Syntax Î½ , syntax-alg {Î½ = Î½}) âŸ¶ (C , alg)) (Î½ â†’ C)
  i .fun = var-alg Î½ C alg
  i .inv k = interp-homo alg k
  i .rightInv k = refl
  i .leftInv (h , comm) = Î£PathP (h-path , comm-path) where
     h-var-fusion-ty : Syntax Î½ â†’ Type
     h-var-fusion-ty x = interp alg (h âˆ˜ var) x â‰¡ h x

     open import Cubical.Foundations.Prelude using (_âˆ™âˆ™_âˆ™âˆ™_)
     open import Cubical.Foundations.Path using (PathPâ‰¡doubleCompPathÊ³)

     h-var-fusion-alg : DepAlg Î½ h-var-fusion-ty
     h-var-fusion-alg (var x) = refl
     h-var-fusion-alg (op oxs PâŸ¨xsâŸ©) = refl âˆ™âˆ™ p-IH âˆ™âˆ™ sym (Î» i â†’ comm i oxs)  where
       p-IH : alg (oxs .fst , (Î» a â†’ interp alg (Î» x â†’ h (var x)) (oxs .snd a))) â‰¡ alg (cmap h oxs)
       p-IH = cong (Î» xs' â†’ alg (oxs .fst , xs')) (funExt (Î» a â†’ PâŸ¨xsâŸ© a))

     h-var-fusion : âˆ€ x â†’ interp alg (h âˆ˜ var) x â‰¡ h x
     h-var-fusion = elim-s h-var-fusion-ty h-var-fusion-alg

     h-path : interp alg (h âˆ˜ var) â‰¡ h
     h-path = funExt h-var-fusion

     -- The key observation is that the h-path for the op case is defined by
     -- composing comm, p-IH, and (interp-homo alg (h âˆ˜ var) .snd) (which is
     -- refl), so there is a trivial filler comm-path of the square formed by
     -- these four paths.
     squeezeSquare : âˆ€{a}{A : Type a}{w x y z : A} (p : w â‰¡ x) {q : x â‰¡ y} (r : z â‰¡ y)
                   â†’ (P : w â‰¡ z) â†’ (sq : P â‰¡ p âˆ™âˆ™ q âˆ™âˆ™ sym r) â†’ I â†’ I â†’ A
     squeezeSquare p {q} r P sq i j = transport (sym (PathPâ‰¡doubleCompPathÊ³ p P q r)) sq i j


     comm-path : PathP (Î» i â†’ (Î» xs â†’ h-path i (op xs)) â‰¡ (Î» xs â†’ alg (cmap (h-path i) xs)))
                       (interp-homo alg (h âˆ˜ var) .snd)
                       comm
     comm-path i j oxs = squeezeSquare (Î» k â†’ (interp-homo alg (h âˆ˜ var) .snd) k oxs )
                                       (Î» k â†’ comm k oxs)
                                       (h-var-fusion (op oxs))
                                       (refl {x = h-var-fusion (op oxs)})
                                       j
                                       i

--------------------------------------------------------------------------------
-- Modality
--------------------------------------------------------------------------------

â—‡â€² : âˆ€ {p} â†’ (A â†’ Type p) â†’ Syntax A â†’ Type p
â—‡â€² P (var x) = P x
â—‡â€² P (op (O , xs)) = âˆƒ i Ã— â—‡â€² P (xs i)

â–¡â€² : âˆ€ {p} â†’ (A â†’ Type p) â†’ Syntax A â†’ Type p
â–¡â€² P (var x) = P x
â–¡â€² P (op (O , xs)) = âˆ€ i â†’ â–¡â€² P (xs i)

â–¡â€²->>=-â–¡â€² : âˆ€ {p q} (P : A â†’ Type p) (Q : B â†’ Type q) â†’
              (xs : Syntax A) (f : A â†’ Syntax B) â†’
              (âˆ€ x â†’ P x â†’ â–¡â€² Q (f x)) â†’ â–¡â€² P xs â†’ â–¡â€² Q (xs >>= f)
â–¡â€²->>=-â–¡â€² P Q (var x) f t Pxs = t x Pxs
â–¡â€²->>=-â–¡â€² P Q (op (Oáµ¢ , xs)) f t Pxs i = â–¡â€²->>=-â–¡â€² P Q (xs i) f t (Pxs i)

â—‡â€²->>=-â—‡â€² : âˆ€ {p q} (P : A â†’ Type p) (Q : B â†’ Type q) â†’
              (xs : Syntax A) (f : A â†’ Syntax B) â†’
              (âˆ€ x â†’ P x â†’ â—‡â€² Q (f x)) â†’ â—‡â€² P xs â†’ â—‡â€² Q (xs >>= f)
â—‡â€²->>=-â—‡â€² P Q (var x) f t Pxs = t x Pxs
â—‡â€²->>=-â—‡â€² P Q (op (Oáµ¢ , xs)) f t (i , Pxs) = i , â—‡â€²->>=-â—‡â€² P Q (xs i) f t Pxs

â–¡â€²-fmap : âˆ€ {A : Type a} {P Q : A â†’ Type p} (t : Syntax A) â†’
            (âˆ€ (x : A) â†’ P x â†’ Q x) â†’ (â–¡â€² P t â†’ â–¡â€² Q t)
â–¡â€²-fmap (var x) f p =  f x p
â–¡â€²-fmap (op (o , k)) f p = Î» a â†’ â–¡â€²-fmap (k a) f (p a)

â—‡â€²-fmap : âˆ€ {A : Type a} {P Q : A â†’ Type p} (t : Syntax A) â†’
            (âˆ€ (x : A) â†’ P x â†’ Q x) â†’ (â—‡â€² P t â†’ â—‡â€² Q t)
â—‡â€²-fmap (var x) f p = f x p
â—‡â€²-fmap (op (o , k)) f (a , p) = a ,  â—‡â€²-fmap (k a) f p

â–¡â€²-shift : âˆ€ {A : Type a} {P : B â†’ Type p} â†’ (f : A â†’ B) â†’ (t : Syntax A) â†’ â–¡â€² (P âˆ˜ f) t â†’ â–¡â€² P (f <$> t)
â–¡â€²-shift f (var x) p = p
â–¡â€²-shift f (op (o , k)) p a = â–¡â€²-shift f (k a) (p a)

â—‡â€²-shift : âˆ€ {A : Type a} {P : B â†’ Type p} â†’ (f : A â†’ B) â†’ (t : Syntax A) â†’ â—‡â€² (P âˆ˜ f) t â†’  â—‡â€² P (f <$> t)
â—‡â€²-shift f (var x) p = p
â—‡â€²-shift f (op (o , k)) (a , p) = a , â—‡â€²-shift f (k a) p

var-inj : Injective (Syntax.var {Î½ = A})
var-inj {x = x} {y} = cong Î» { (var x) â†’ x ; (op _) â†’ x }

op-inj : Injective (Syntax.op {Î½ = A})
op-inj {x = x} {y} = cong Î» { (var _) â†’ x ; (op x) â†’ x }

open import Cubical.Data.Sigma.Properties

opF-inj : isSet Op â†’ (O : Op) â†’ (xs ys : Arity O â†’ Syntax A) â†’ (op (O , xs) â¦‚ Syntax A) â‰¡ op (O , ys) â†’ xs â‰¡ ys
opF-inj set O xs ys p =
  Î£-contractFst (refl , Î» _ â†’ set _ _ _ _) .fst (PathPÎ£ (op-inj p))

isReturn : Syntax A â†’ Bool
isReturn (var _) = true
isReturn (op _) = false

fmap-injective : (f : A â†’ B) â†’ Injective f â†’ Injective (f <$>_)
fmap-injective f inj {var x} {var y} p = cong var (inj (var-inj p))
fmap-injective {A = A} {B = B} f inj {op (i , x)} {op (j , y)} p =
  let q = op-inj { x = i , _<$>_ f âˆ˜ x } { y = j , _<$>_ f âˆ˜ y } p
      h = cong fst q
  in cong (op â¦‚ (âŸ¦ ğ”½ âŸ§ (Syntax _) â†’ Syntax _))
    (congâ‚‚ _,_ h
    (J (Î» z zp â†’ âˆ€ y â†’ PathP (Î» l â†’ Arity (zp l) â†’ Syntax B) (_<$>_ f âˆ˜ x) (_<$>_ f âˆ˜ y) â†’ PathP (Î» l â†’ Arity (zp l) â†’ Syntax A) x y)
        (Î» yâ€² pâ€² â†’ funExt Î» k â†’ fmap-injective f inj {x k} {yâ€² k} (cong (_$ k) pâ€²)) h y (cong snd q)))
fmap-injective f inj {var x} {op y} p = absurd (trueâ‰¢false (cong isReturn p))
fmap-injective f inj {op x} {var y} p = absurd (falseâ‰¢true (cong isReturn p))

varâ‰¢op : âˆ€ {x : A} {O k} â†’ var x â‰¢ op (O , k)
varâ‰¢op = trueâ‰¢false âˆ˜ cong isReturn

opâ‰¢var : âˆ€ {x : A} {O k} â†’ op (O , k) â‰¢ var x
opâ‰¢var = falseâ‰¢true âˆ˜ cong isReturn

>>=-com-<$> : (f : A â†’ Syntax B) (g : B â†’ C) (xs : Syntax A) â†’ (xs >>= Î» x â†’ g <$> f x) â‰¡ g <$> (xs >>= f)
>>=-com-<$> f g = elim-s _ Î» { (var x) â†’ refl ; (op (O , k) PâŸ¨xsâŸ©) â†’ cong (op âˆ˜ _,_ O) (funExt PâŸ¨xsâŸ©) }
