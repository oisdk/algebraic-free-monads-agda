{-# OPTIONS --safe --lossy-unification #-}

open import Prelude
open import Signatures
open import FinitenessConditions
open import FreeMonad.PackagedTheory

module Hoare.Calculus {â„“} (ğ’¯ : FullTheory â„“) where

open import FreeMonad.Quotiented ğ’¯
open import Hoare.Definition ğ’¯
open import Hoare.Lemmas ğ’¯
open import Truth

âŸ¨return_âŸ© : Î© a â†’ Assertion a
âŸ¨return x âŸ© .fst = return x
âŸ¨return x âŸ© .snd .fst k = refl
âŸ¨return x âŸ© .snd .snd k = refl

pass : âˆ€ {a} â†’ Term (Poly-âŠ¤ {a})
pass = return Poly-tt

_<<_ : Term A â†’ Term B â†’ Term A
xs << ys = do x â† xs; ys; return x

sef :      (Î¦ : Assertion a) â†’ let Ï• = Î¦ .fst in
           (q : Term A) 

    â†’                  SEF 0 q
    â†’     --------------------------------
                   âŸ… Ï• âŸ† q >> pass âŸ… Ï• âŸ†

sef (Ï• , sefâŸ¨Ï•âŸ© , dsefâŸ¨Ï•âŸ©) q sefâŸ¨qâŸ© .proof k =

  (do a â† Ï•
      q >> return Poly-tt
      b â† Ï•
      k Poly-tt (a |â†’| b)) â‰¡âŸ¨ a â‡ Ï• â¨¾/ cong (_>> _) (sefâŸ¨qâŸ© _) âŸ©

  (do a â† Ï•
      b â† Ï•
      k Poly-tt (a |â†’| b)) â‰¡âŸ¨ dsefâŸ¨Ï•âŸ© (Î» a b â†’ k Poly-tt (a |â†’| b)) âŸ©

  (do a â† Ï•
      b â† Ï•
      k Poly-tt (a |â†’| a)) â‰¡âŸ¨ a â‡ Ï• â¨¾/ b â‡ Ï• â¨¾/ cong (k Poly-tt) (|â†’|-id a) âŸ©

  (do a â† Ï•
      b â† Ï•
      k Poly-tt True) â‰¡Ë˜âŸ¨ a â‡ Ï• â¨¾/ cong (_>> _) (sefâŸ¨qâŸ© _) âŸ©

  (do a â† Ï•
      x â† q >> return Poly-tt
      b â† Ï•
      k Poly-tt True) âˆ

stateless : (Ï• : Î© a) (p : Term B) â†’ âŸ… return Ï• âŸ† [ p /âŸ¨âŸ©] âŸ… return Ï• âŸ†
stateless Ï• p .proof k = cong ([ p /âŸ¨âŸ©] >>_) (cong (k tt) (|â†’|-id Ï•))

dsef : {A : Type a}   â†’              (p : Term A)

                      â†’                  DET (â„“suc a) p
                      â†’ -------------------------------------------------
                          âŸ… return (True {a}) âŸ† x â† p âŸ… (do y â† p ; return (x |â‰¡| y)) âŸ†

dsef {a = aâ„“} p dsefâŸ¨pâŸ© .proof k =

    (do a â† return (True {aâ„“})
        x â† p
        b â† (do y â† p ; return (x |â‰¡| y))
        k x (a |â†’| b)) â‰¡âŸ¨âŸ©

    (do x â† p
        b â† (do y â† p ; return (x |â‰¡| y))
        k x (True {aâ„“} |â†’| b)) â‰¡âŸ¨ x â‡ p â¨¾/ assoc p _ _ âŸ©

    (do x â† p
        y â† p
        b â† return (x |â‰¡| y)
        k x (True {aâ„“} |â†’| b)) â‰¡âŸ¨âŸ©

    (do x â† p
        y â† p
        k x (True {aâ„“} |â†’| (x |â‰¡| y)))

          â‰¡âŸ¨ x â‡ p â¨¾/ y â‡ p â¨¾/ cong (k x) (|â†’|-idË¡ (x |â‰¡| y)) âŸ©

    (do x â† p
        y â† p
        k x (x |â‰¡| y)) â‰¡âŸ¨ dsefâŸ¨pâŸ© _ âŸ©

    (do x â† p
        y â† p
        k x (x |â‰¡| x))

          â‰¡âŸ¨ x â‡ p â¨¾/ y â‡ p â¨¾/ cong (k x) (proves âˆ£ refl âˆ£) âŸ©

    (do x â† p
        y â† p
        k x (True {aâ„“})) â‰¡Ë˜âŸ¨ dsefâŸ¨pâŸ© _ âŸ©

    (do x â† p
        y â† p
        k x (True {aâ„“})) â‰¡âŸ¨âŸ©

    (do a â† return (True {aâ„“})
        x â† p
        y â† p
        k x (True {aâ„“})) â‰¡Ë˜âŸ¨ x â‡ p â¨¾/ assoc p _ _ âŸ©

    (do a â† return (True {aâ„“})
        x â† p
        b â† (do y â† p ; return (x |â‰¡| y))
        k x (True {aâ„“})) âˆ

âŸ¨&&&âŸ©Ë¡ :
  (Ï• : Term (Î© b))
  (Ïˆ : A â†’ Term (Î© b))
  (Ï‡ : Î© b)
  (t : Term A) â†’
  âŸ… Ï• âŸ† x â† t âŸ… Ïˆ x âŸ† â†’
  âŸ… return Ï‡ âŸ¨âˆ§âŸ© Ï• âŸ† x â† t âŸ… return Ï‡ âŸ¨âˆ§âŸ© Ïˆ x âŸ†
âŸ¨&&&âŸ©Ë¡ Ï• Ïˆ Ï‡ t h .proof k =

  (do a â† return Ï‡ âŸ¨âˆ§âŸ© Ï•
      x â† t
      b â† return Ï‡ âŸ¨âˆ§âŸ© Ïˆ x
      k x (a |â†’| b)) â‰¡âŸ¨ âŸªassocâŸ« (return Ï‡) Ï• _|âˆ§|_ _ âŸ©

  (do a â† Ï•
      x â† t
      b â† return Ï‡ âŸ¨âˆ§âŸ© Ïˆ x
      k x ((Ï‡ |âˆ§| a) |â†’| b)) â‰¡âŸ¨ (a â‡ Ï• â¨¾/ x â‡ t â¨¾/ âŸªassocâŸ« (return Ï‡) (Ïˆ x) _|âˆ§|_ _) âŸ©

  (do a â† Ï•
      x â† t
      b â† Ïˆ x
      k x ((Ï‡ |âˆ§| a) |â†’| (Ï‡ |âˆ§| b))) â‰¡âŸ¨ a â‡ Ï• â¨¾/ x â‡ t â¨¾/ b â‡ Ïˆ x â¨¾/ cong (k x) ((Î» f x a â†’ snd (f (x , a))) iff Î» { f (x , a) â†’ x , f x a }) âŸ©

  (do a â† Ï•
      x â† t
      b â† Ïˆ x
      k x (Ï‡ |â†’| (a |â†’| b))) â‰¡âŸ¨ h .proof (Î» x r â†’ k x (Ï‡ |â†’| r)) âŸ©

  (do a â† Ï•
      x â† t
      b â† Ïˆ x
      k x (Ï‡ |â†’| True)) â‰¡âŸ¨ a â‡ Ï• â¨¾/ x â‡ t â¨¾/ b â‡ Ïˆ x â¨¾/ cong (k x) (|â†’|-annÊ³ Ï‡) âŸ©

  (do a â† Ï•
      x â† t
      b â† Ïˆ x
      k x True) â‰¡Ë˜âŸ¨ a â‡ Ï• â¨¾/ x â‡ t â¨¾/ âŸªassocâŸ« (return Ï‡) (Ïˆ x) _|âˆ§|_ _ âŸ©

  (do a â† Ï•
      x â† t
      b â† return Ï‡ âŸ¨âˆ§âŸ© Ïˆ x
      k x True) â‰¡Ë˜âŸ¨ âŸªassocâŸ« (return Ï‡) Ï• _|âˆ§|_ _ âŸ©

  (do a â† return Ï‡ âŸ¨âˆ§âŸ© Ï•
      x â† t
      b â† return Ï‡ âŸ¨âˆ§âŸ© Ïˆ x
      k x True) âˆ

_â¨¾,_ : Term A â†’ (A â†’ Term B) â†’ Term (A Ã— B)
xs â¨¾, ys = do
  x â† xs
  y â† ys x
  return (x , y)

â¨¾,-assoc : (xs : Term A) (ys : A â†’ Term B) (k : A â†’ B â†’ Term C) â†’
           (xs â¨¾, ys) >>= uncurry k â‰¡ (do x â† xs ; y â† ys x ; k x y)
â¨¾,-assoc xs ys k = assoc xs (Î» x â†’ ys x >>= Î» y â†’ return (x , y)) (uncurry k) â¨¾ x â‡ xs â¨¾/ assoc (ys x) _ _

seq : {A B : Type a}

    â†’                (Î¦ : Assertion a)         â†’ let Ï• = Î¦ .fst in
                     (Î¨ : A â†’ Assertion a)     â†’ let Ïˆ = fst âˆ˜ Î¨ in
                     (Î§ : A Ã— B â†’ Assertion a) â†’ let Ï‡ = fst âˆ˜ Î§ in
                            {p : Term A}    {q : A â†’ Term B}

    â†’                               âŸ… Ï• âŸ† x â† p âŸ… Ïˆ x âŸ†
    â†’                      (âˆ€ x â†’ âŸ… Ïˆ x âŸ† y â† q x âŸ… Ï‡ (x , y) âŸ†)
    â†’              ------------------------------------------------
                             âŸ… Ï• âŸ† xy â† p â¨¾, q âŸ… Ï‡ xy âŸ†

seq {a = aâ„“} (Ï• , sefâŸ¨Ï•âŸ© , dsefâŸ¨Ï•âŸ©) Î¨ Î§ {p = p} {q = q} lhs rhs .proof k =

  let Ï‡ = fst âˆ˜ Î§
      Ïˆ = fst âˆ˜ Î¨
  in

  (do a â† Ï•
      (x , y) â† p â¨¾, q
      b â† Ï‡ (x , y)
      k (x , y) (a |â†’| b))

        â‰¡âŸ¨ a â‡ Ï• â¨¾/ â¨¾,-assoc p q _ âŸ©

  (do a â† Ï•
      x â† p
      y â† q x
      b â† Ï‡ (x , y)
      k (x , y) (a |â†’| b))

        â‰¡Ë˜âŸ¨ a â‡ Ï• â¨¾/ x â‡ p â¨¾/ Î¨ x .snd .fst _ âŸ©

  (do a â† Ï•
      x â† p
      i â† Ïˆ x
      y â† q x
      b â† Ï‡ (x , y)
      k (x , y) (a |â†’| b))

        â‰¡Ë˜âŸ¨ a â‡ Ï• â¨¾/ x â‡ p â¨¾/ i â‡ Ïˆ x â¨¾/ y â‡ q x â¨¾/ b â‡ Ï‡ (x , y) â¨¾/ cong (k (x , y)) (|â†’|-idË¡ _) âŸ©

  (do a â† Ï•
      x â† p
      i â† Ïˆ x
      y â† q x
      b â† Ï‡ (x , y)
      k (x , y) (True {aâ„“} |â†’| (a |â†’| b)))

        â‰¡Ë˜âŸ¨ a â‡ Ï• â¨¾/ x â‡ p â¨¾/ i â‡ Ïˆ x â¨¾/ y â‡ q x â¨¾/ sef-dsef-cont (Î§ (x , y)) _ âŸ©

  (do a â† Ï•
      x â† p
      i â† Ïˆ x
      y â† q x
      bâ‚ â† Ï‡ (x , y)
      bâ‚‚ â† Ï‡ (x , y)
      k (x , y) (True {aâ„“} |â†’| (a |â†’| bâ‚‚)))

        â‰¡Ë˜âŸ¨ a â‡ Ï• â¨¾/ x â‡ p â¨¾/ rhs x .proof (Î» y r â†’ do bâ‚‚ â† Ï‡ (x , y) ; k (x , y) (r |â†’| a |â†’| bâ‚‚) ) âŸ©

  (do a â† Ï•
      x â† p
      i â† Ïˆ x
      y â† q x
      bâ‚ â† Ï‡ (x , y)
      bâ‚‚ â† Ï‡ (x , y)
      k (x , y) ((i |â†’| bâ‚) |â†’| (a |â†’| bâ‚‚)))

        â‰¡âŸ¨ a â‡ Ï• â¨¾/ x â‡ p â¨¾/ i â‡ Ïˆ x â¨¾/ y â‡ q x â¨¾/ sef-dsef-cont (Î§ (x , y)) _ âŸ©

  (do a â† Ï•
      x â† p
      i â† Ïˆ x
      y â† q x
      b â† Ï‡ (x , y)
      k (x , y) ((i |â†’| b) |â†’| (a |â†’| b)))

        â‰¡Ë˜âŸ¨ a â‡ Ï• â¨¾/ x â‡ p â¨¾/ i â‡ Ïˆ x â¨¾/ y â‡ q x â¨¾/ b â‡ Ï‡ (x , y) â¨¾/ cong (k (x , y)) (|â†’|-idË¡ _) âŸ©

  (do a â† Ï•
      x â† p
      i â† Ïˆ x
      y â† q x
      b â† Ï‡ (x , y)
      k (x , y) (True {aâ„“} |â†’| (i |â†’| b) |â†’| (a |â†’| b)))

        â‰¡Ë˜âŸ¨ sef-dsef-cont (Ï• , sefâŸ¨Ï•âŸ© , dsefâŸ¨Ï•âŸ©) _ âŸ©

  (do aâ‚ â† Ï•
      aâ‚‚ â† Ï•
      x â† p
      i â† Ïˆ x
      y â† q x
      b â† Ï‡ (x , y)
      k (x , y) (True {aâ„“} |â†’| (i |â†’| b) |â†’| (aâ‚ |â†’| b)))

        â‰¡Ë˜âŸ¨ aâ‚ â‡ Ï• â¨¾/ aâ‚‚ â‡ Ï• â¨¾/ x â‡ p â¨¾/ sef-dsef-cont (Î¨ x) _ âŸ©

  (do aâ‚ â† Ï•
      aâ‚‚ â† Ï•
      x â† p
      iâ‚ â† Ïˆ x
      iâ‚‚ â† Ïˆ x
      y â† q x
      b â† Ï‡ (x , y)
      k (x , y) (True {aâ„“} |â†’| (iâ‚‚ |â†’| b) |â†’| (aâ‚ |â†’| b)))

        â‰¡Ë˜âŸ¨ aâ‚ â‡ Ï• â¨¾/ proof lhs (Î» x r â†’ do iâ‚‚ â† Ïˆ x ; y â† q x ; b â† Ï‡ (x , y) ; k (x , y) (r |â†’| (iâ‚‚ |â†’| b) |â†’| (aâ‚ |â†’| b))) âŸ©

  (do aâ‚ â† Ï•
      aâ‚‚ â† Ï•
      x â† p
      iâ‚ â† Ïˆ x
      iâ‚‚ â† Ïˆ x
      y â† q x
      b â† Ï‡ (x , y)
      k (x , y) ((aâ‚‚ |â†’| iâ‚) |â†’| (iâ‚‚ |â†’| b) |â†’| (aâ‚ |â†’| b)))

        â‰¡âŸ¨ sef-dsef-cont (Ï• , sefâŸ¨Ï•âŸ© , dsefâŸ¨Ï•âŸ©) _ âŸ©

  (do a â† Ï•
      x â† p
      iâ‚ â† Ïˆ x
      iâ‚‚ â† Ïˆ x
      y â† q x
      b â† Ï‡ (x , y)
      k (x , y) ((a |â†’| iâ‚) |â†’| (iâ‚‚ |â†’| b) |â†’| (a |â†’| b)))

        â‰¡âŸ¨ a â‡ Ï• â¨¾/ x â‡ p â¨¾/ sef-dsef-cont (Î¨ x) _ âŸ©

  (do a â† Ï•
      x â† p
      i â† Ïˆ x
      y â† q x
      b â† Ï‡ (x , y)
      k (x , y) ((a |â†’| i) |â†’| (i |â†’| b) |â†’| (a |â†’| b)))

        â‰¡Ë˜âŸ¨ a â‡ Ï• â¨¾/ x â‡ p â¨¾/ i â‡ Ïˆ x â¨¾/ y â‡ q x â¨¾/ b â‡ Ï‡ (x , y) â¨¾/ cong (k (x , y)) (|â†’|-curry _ _ _) âŸ©

  (do a â† Ï•
      x â† p
      i â† Ïˆ x
      y â† q x
      b â† Ï‡ (x , y)
      k (x , y) ((a |â†’| i) |âˆ§| (i |â†’| b) |â†’| (a |â†’| b)))

        â‰¡âŸ¨ a â‡ Ï• â¨¾/ x â‡ p â¨¾/ i â‡ Ïˆ x â¨¾/ y â‡ q x â¨¾/ b â‡ Ï‡ (x , y) â¨¾/ cong (k (x , y)) (|â†’|-trans a i b) âŸ©

  (do a â† Ï•
      x â† p
      i â† Ïˆ x
      y â† q x
      b â† Ï‡ (x , y)
      k (x , y) (True {aâ„“}))

        â‰¡âŸ¨ a â‡ Ï• â¨¾/ x â‡ p â¨¾/ Î¨ x .snd .fst _ âŸ©

  (do a â† Ï•
      x â† p
      y â† q x
      b â† Ï‡ (x , y)
      k (x , y) True)

        â‰¡Ë˜âŸ¨ a â‡ Ï• â¨¾/ â¨¾,-assoc p q _ âŸ©

  (do a â† Ï•
      (x , y) â† p â¨¾, q
      b â† Ï‡ (x , y)
      k (x , y) True) âˆ



focus : {A B : Type a} {Ï• : Term (Î© b)} {Ïˆ : B â†’ Term (Î© c)} {p : Term A}
      â†’ (sub : A â†’ B)
      â†’ âŸ… Ï• âŸ† x â† p âŸ… Ïˆ (sub x) âŸ†
      â†’ âŸ… Ï• âŸ† x â† sub <$> p âŸ… Ïˆ x âŸ†
focus {Ï• = Ï•} {Ïˆ = Ïˆ} {p = p} sub hr .proof k =
  (do a â† Ï•
      x â† sub <$> p
      b â† Ïˆ x
      k x (a |â†’| b)) â‰¡âŸ¨ a â‡ Ï• â¨¾/ assoc p _ _ âŸ©

  (do a â† Ï•
      x â† p
      b â† Ïˆ (sub x)
      k (sub x) (a |â†’| b)) â‰¡âŸ¨ hr .proof (k âˆ˜ sub) âŸ©

  (do a â† Ï•
      x â† p
      b â† Ïˆ (sub x)
      k (sub x) True) â‰¡Ë˜âŸ¨ a â‡ Ï• â¨¾/ assoc p _ _ âŸ©

  (do a â† Ï•
      x â† sub <$> p
      b â† Ïˆ x
      k x True) âˆ

â‰¡âŸ…â‰¡âŸ†â‰¡ : {Ï• Ï•â€² : Term (Î© a)} {t tâ€² : Term A} {Ïˆ Ïˆâ€² : A â†’ Term (Î© b)} â†’ Ï• â‰¡ Ï•â€² â†’ t â‰¡ tâ€² â†’ (âˆ€ x â†’ Ïˆ x â‰¡ Ïˆâ€² x) â†’ âŸ… Ï• âŸ† x â† t âŸ… Ïˆ x âŸ† â†’ âŸ… Ï•â€² âŸ† x â† tâ€² âŸ… Ïˆâ€² x âŸ†
â‰¡âŸ…â‰¡âŸ†â‰¡ p q r = subst (Hoare _ _) (funExt r) âˆ˜ substâ‚‚ (Î» x y â†’ Hoare x y _) p q

subst-âŸ…âŸ† : {Ï• : Term (Î© b)} {p : Term A} {Ïˆ : A â†’ Term (Î© c)} {q : Term A}
        â†’ p â‰¡ q
        â†’ âŸ… Ï• âŸ† x â† p âŸ… Ïˆ x âŸ†
        â†’ âŸ… Ï• âŸ† x â† q âŸ… Ïˆ x âŸ†
subst-âŸ…âŸ† {Ï• = Ï•} {Ïˆ = Ïˆ} = subst (flip (Hoare Ï•) Ïˆ)

seqâ€² : {A B : Type a}
    â†’                (Î¦ : Assertion a)     â†’ let Ï• = Î¦ .fst in
                     (Î¨ : A â†’ Assertion a) â†’ let Ïˆ = fst âˆ˜ Î¨ in
                     (Î§ : B â†’ Assertion a) â†’ let Ï‡ = fst âˆ˜ Î§ in
                            {p : Term A}    {q : A â†’ Term B}

    â†’                               âŸ… Ï• âŸ† x â† p âŸ… Ïˆ x âŸ†
    â†’                      (âˆ€ x â†’ âŸ… Ïˆ x âŸ† y â† q x âŸ… Ï‡ y âŸ†)
    â†’              ------------------------------------------------
                             âŸ… Ï• âŸ† y â† p >>= q âŸ… Ï‡ y âŸ†
seqâ€² Î¦ Î¨ Î§ {p = p} {q = q} lhs rhs =
  let h  = seq Î¦ Î¨ (Î§ âˆ˜ snd) lhs rhs
      hâ€² = focus snd h
  in subst-âŸ…âŸ† (snd <$> (p â¨¾, q) â‰¡âŸ¨ assoc p  _ _ â¨¾ x â‡ p â¨¾/ assoc (q x) _ _ â¨¾ >>=-idÊ³ (q x) âŸ© (p >>= q) âˆ) hâ€²

seqâ» : {A B : Type a}
    â†’                (Î¦ : Assertion a)     â†’ let Ï• = Î¦ .fst in
                     (Î¨ : A â†’ Assertion a) â†’ let Ïˆ = fst âˆ˜ Î¨ in
                     (Î§ : B â†’ Assertion a) â†’ let Ï‡ = fst âˆ˜ Î§ in
                            {p : Term A}    {q : A â†’ Term B}

    â†’                      (âˆ€ x â†’ âŸ… Ïˆ x âŸ† y â† q x âŸ… Ï‡ y âŸ†)
    â†’                               âŸ… Ï• âŸ† x â† p âŸ… Ïˆ x âŸ†
    â†’              ------------------------------------------------
                             âŸ… Ï• âŸ† y â† p >>= q âŸ… Ï‡ y âŸ†
seqâ» Î¦ Î¨ Î§ = flip (seqâ€² Î¦ Î¨ Î§)

seq-<< : {A B : Type a}
    â†’                (Î¦ : Assertion a)     â†’ let Ï• = Î¦ .fst in
                     (Î¨ : A â†’ Assertion a) â†’ let Ïˆ = fst âˆ˜ Î¨ in
                     (Î§ : A â†’ Assertion a) â†’ let Ï‡ = fst âˆ˜ Î§ in
                            {p : Term A}    {q : Term B}

    â†’                               âŸ… Ï• âŸ† x â† p âŸ… Ïˆ x âŸ†
    â†’                      (âˆ€ x â†’ âŸ… Ïˆ x âŸ† y â† q âŸ… Ï‡ x âŸ†)
    â†’              ------------------------------------------------
                             âŸ… Ï• âŸ† y â† p << q âŸ… Ï‡ y âŸ†
seq-<< Î¦ Î¨ Î§ {p = p} {q = q} lhs rhs =
  let h = seq Î¦ Î¨ (Î§ âˆ˜ fst) lhs rhs
      hâ€² = focus fst h
  in subst-âŸ…âŸ† (assoc p _ _ â¨¾ x â‡ p â¨¾/ assoc q _ _) hâ€²


module _
  {A : Type a}
  (Ï• : Assertion a)
  (p : Term A)
  (Ïˆ Ï‡ : A â†’ Assertion a)
  where
    imply : (âˆ€ x â†’ âŸ… Ïˆ x .fst âŸ† pass âŸ… Ï‡ x .fst âŸ†)
          â†’ âŸ… Ï• .fst âŸ† x â† p âŸ… Ïˆ x .fst âŸ†
          â†’ âŸ… Ï• .fst âŸ† x â† p âŸ… Ï‡ x .fst âŸ†
    imply imp h = subst-âŸ…âŸ† (>>=-idÊ³ p) ( seqâ€² Ï• Ïˆ Ï‡ h Î» x â†’ focus (const x) (imp x))

âŸ…sndâŸ† : {A : Type a} (Î¦ : Assertion a) (p : Term A)
        (Ïˆâ‚ : A â†’ Term (Î© a)) â†’
        (sefâŸ¨Ïˆâ‚âŸ© : âˆ€ x â†’ SEF (â„“suc a) (Ïˆâ‚ x))
        (Î¨â‚‚ : A â†’ Assertion a) â†’
      âŸ… Î¦ .fst âŸ† x â† p âŸ… Ïˆâ‚ x âŸ¨âˆ§âŸ© Î¨â‚‚ x .fst âŸ† â†’ âŸ… Î¦ .fst âŸ† x â† p âŸ… Î¨â‚‚ x .fst âŸ†
âŸ…sndâŸ† Î¦ p Ïˆâ‚ sefâŸ¨Ïˆâ‚âŸ© Î¨â‚‚ prf .proof k =
  let Ï• = Î¦ .fst
      Ïˆâ‚‚ = fst âˆ˜ Î¨â‚‚
  in

  (do a â† Ï•
      x â† p
      bâ‚‚ â† Ïˆâ‚‚ x
      k x (a |â†’| bâ‚‚)) â‰¡Ë˜âŸ¨ a â‡ Ï• â¨¾/ Î¦ .snd .fst _ âŸ©

  (do a â† Ï•
      aâ€² â† Ï•
      x â† p
      bâ‚‚ â† Ïˆâ‚‚ x
      k x (a |â†’| bâ‚‚)) â‰¡Ë˜âŸ¨ a â‡ Ï• â¨¾/ aâ€² â‡ Ï• â¨¾/ x â‡ p â¨¾/ sefâŸ¨Ïˆâ‚âŸ© x _ âŸ©

  (do a â† Ï•
      aâ€² â† Ï•
      x â† p
      bâ‚â€² â† Ïˆâ‚ x
      bâ‚‚ â† Ïˆâ‚‚ x
      k x (a |â†’| bâ‚‚)) â‰¡Ë˜âŸ¨ a â‡ Ï• â¨¾/ aâ€² â‡ Ï• â¨¾/ x â‡ p â¨¾/ bâ‚â€² â‡ Ïˆâ‚ x â¨¾/ Î¨â‚‚ x .snd .fst _ âŸ©

  (do a â† Ï•
      aâ€² â† Ï•
      x â† p
      bâ‚â€² â† Ïˆâ‚ x
      bâ‚‚â€² â† Ïˆâ‚‚ x
      bâ‚‚ â† Ïˆâ‚‚ x
      k x (a |â†’| bâ‚‚)) â‰¡Ë˜âŸ¨ a â‡ Ï• â¨¾/ aâ€² â‡ Ï• â¨¾/ x â‡ p â¨¾/ âŸªassocâŸ« (Ïˆâ‚ x) (Ïˆâ‚‚ x) _|âˆ§|_ _ âŸ©

  (do a â† Ï•
      aâ€² â† Ï•
      x â† p
      bâ€² â† Ïˆâ‚ x âŸ¨âˆ§âŸ© Ïˆâ‚‚ x
      bâ‚‚ â† Ïˆâ‚‚ x
      k x (a |â†’| bâ‚‚)) â‰¡âŸ¨ a â‡ Ï• â¨¾/ aâ€² â‡ Ï• â¨¾/ x â‡ p â¨¾/ bâ€² â‡ Ïˆâ‚ x âŸ¨âˆ§âŸ© Ïˆâ‚‚ x â¨¾/ bâ‚‚ â‡ Ïˆâ‚‚ x â¨¾/ cong (k x) (cong (a |â†’|_) ((_, _) iff fst)) âŸ©

  (do a â† Ï•
      aâ€² â† Ï•
      x â† p
      bâ€² â† Ïˆâ‚ x âŸ¨âˆ§âŸ© Ïˆâ‚‚ x
      bâ‚‚ â† Ïˆâ‚‚ x
      k x (a |â†’| (bâ‚‚ |âˆ§| True))) â‰¡Ë˜âŸ¨ a â‡ Ï• â¨¾/  prf .proof (Î» x t â†’ do bâ‚‚ â† Ïˆâ‚‚ x; k x (a |â†’| (bâ‚‚ |âˆ§| t))) âŸ©

  (do a â† Ï•
      aâ€² â† Ï•
      x â† p
      bâ€² â† Ïˆâ‚ x âŸ¨âˆ§âŸ© Ïˆâ‚‚ x
      bâ‚‚ â† Ïˆâ‚‚ x
      k x (a |â†’| (bâ‚‚ |âˆ§| (aâ€² |â†’| bâ€²)))) â‰¡âŸ¨ sef-dsef-cont Î¦ _ âŸ©

  (do a â† Ï•
      x â† p
      bâ€² â† Ïˆâ‚ x âŸ¨âˆ§âŸ© Ïˆâ‚‚ x
      bâ‚‚ â† Ïˆâ‚‚ x
      k x (a |â†’| (bâ‚‚ |âˆ§| (a |â†’| bâ€²)))) â‰¡âŸ¨ (a â‡ Ï• â¨¾/ x â‡ p â¨¾/ âŸªassocâŸ« (Ïˆâ‚ x) (Ïˆâ‚‚ x) _|âˆ§|_ _ ) âŸ©

  (do a â† Ï•
      x â† p
      bâ‚ â† Ïˆâ‚ x
      bâ‚‚â€² â† Ïˆâ‚‚ x
      bâ‚‚ â† Ïˆâ‚‚ x
      k x (a |â†’| (bâ‚‚ |âˆ§| (a |â†’| (bâ‚ |âˆ§| bâ‚‚â€²))))) â‰¡âŸ¨ a â‡ Ï• â¨¾/ x â‡ p â¨¾/ bâ‚ â‡ Ïˆâ‚ x â¨¾/ sef-dsef-cont (Î¨â‚‚ x) _ âŸ©

  (do a â† Ï•
      x â† p
      bâ‚ â† Ïˆâ‚ x
      bâ‚‚ â† Ïˆâ‚‚ x
      k x (a |â†’| (bâ‚‚ |âˆ§| (a |â†’| (bâ‚ |âˆ§| bâ‚‚))))) â‰¡âŸ¨ a â‡ Ï• â¨¾/ x â‡ p â¨¾/ bâ‚ â‡ Ïˆâ‚ x â¨¾/ bâ‚‚ â‡ Ïˆâ‚‚ x â¨¾/ cong (k x) ((Î» f x â†’ f x .snd x .fst , f x .fst) iff Î» f x â†’ f x .snd , const (f x)) âŸ©

  (do a â† Ï•
      x â† p
      bâ‚ â† Ïˆâ‚ x
      bâ‚‚ â† Ïˆâ‚‚ x
      k x (a |â†’| (bâ‚ |âˆ§| bâ‚‚))) â‰¡Ë˜âŸ¨ a â‡ Ï• â¨¾/ x â‡ p â¨¾/ âŸªassocâŸ« (Ïˆâ‚ x) (Ïˆâ‚‚ x) _|âˆ§|_ _ âŸ©

  (do a â† Ï•
      x â† p
      bâ‚âˆ§bâ‚‚ â† Ïˆâ‚ x âŸ¨âˆ§âŸ© Ïˆâ‚‚ x
      k x (a |â†’| bâ‚âˆ§bâ‚‚)) â‰¡âŸ¨ prf .proof k âŸ©

  (do a â† Ï•
      x â† p
      bâ‚âˆ§bâ‚‚ â† Ïˆâ‚ x âŸ¨âˆ§âŸ© Ïˆâ‚‚ x
      k x True) â‰¡âŸ¨ a â‡ Ï• â¨¾/ x â‡ p â¨¾/ âŸªassocâŸ« (Ïˆâ‚ x) (Ïˆâ‚‚ x) _|âˆ§|_ _ âŸ©

  (do a â† Ï•
      x â† p
      bâ‚ â† Ïˆâ‚ x
      bâ‚‚ â† Ïˆâ‚‚ x
      k x True) â‰¡âŸ¨ a â‡ Ï• â¨¾/ x â‡ p â¨¾/  sefâŸ¨Ïˆâ‚âŸ© x _ âŸ©


  (do a â† Ï•
      x â† p
      bâ‚‚ â† Ïˆâ‚‚ x
      k x True) âˆ

âŸ…â†’âŸ†True : (Ï• : Term (Î© a)) (t : Term B) â†’ âŸ… Ï• âŸ† _ â† t âŸ… return (True {â„“ = c}) âŸ†
âŸ…â†’âŸ†True Ï• t .proof k = a â‡ Ï• â¨¾/ x â‡ t â¨¾/ cong (k x) (|â†’|-annÊ³ _)


module _ {A : Type a} (Î¦ : Assertion a) {t : Term A} {Ïˆ : A â†’ Term (Î© a)} (f : Î© a â†’ Î© a) where
  private Ï• = fst Î¦

  â†’âŸ…âˆ™âŸ† : (i : âˆ€ {x} â†’ ProofOf (f x) â†’ ProofOf x)
       â†’ âŸ… Ï• âŸ† x â† t âŸ… Ïˆ x âŸ†
       â†’ âŸ… f <$> Ï• âŸ† x â† t âŸ… Ïˆ x âŸ†
  â†’âŸ…âˆ™âŸ† i h .proof k =
    (do a â† f <$> Ï•
        x â† t
        b â† Ïˆ x
        k x (a |â†’| b))

      â‰¡âŸ¨ assoc Ï• _ _ âŸ©

    (do a â† Ï•
        x â† t
        b â† Ïˆ x
        k x (f a |â†’| b))

      â‰¡âŸ¨ a â‡ Ï• â¨¾/ x â‡ t â¨¾/ b â‡ Ïˆ x â¨¾/ cong (k x) ((Î» k x y â†’ k x) iff Î» k x â†’ k x (i x)) âŸ©

    (do a â† Ï•
        x â† t
        b â† Ïˆ x
        k x (f a |â†’| a |â†’| b))

      â‰¡Ë˜âŸ¨ sef-dsef-cont Î¦ _ âŸ©

    (do a  â† Ï•
        aâ€² â† Ï•
        x â† t
        b â† Ïˆ x
        k x (f a |â†’| aâ€² |â†’| b))

      â‰¡âŸ¨ a â‡ Ï• â¨¾/ h .proof (Î» x r â†’ k x (f a |â†’| r)) âŸ©

    (do a  â† Ï•
        aâ€² â† Ï•
        x â† t
        b â† Ïˆ x
        k x (f a |â†’| True))

      â‰¡âŸ¨ sef-dsef-cont Î¦ _ âŸ©

    (do a â† Ï•
        x â† t
        b â† Ïˆ x
        k x (f a |â†’| True))

      â‰¡âŸ¨ a â‡ Ï• â¨¾/ x â‡ t â¨¾/ b â‡ Ïˆ x â¨¾/ cong (k x) (|â†’|-annÊ³ (f a)) âŸ©

    (do a â† Ï•
        x â† t
        b â† Ïˆ x
        k x True)

      â‰¡Ë˜âŸ¨ assoc Ï• _ _ âŸ©

    (do a â† f <$> Ï•
        x â† t
        b â† Ïˆ x
        k x True) âˆ

module _ {A : Type a} (Î¦ : Assertion a) {t : Term A} (Î¨ : A â†’ Assertion a) (f : Î© a â†’ Î© a) where
  private
    Ïˆ = fst âˆ˜ Î¨
    Ï• = fst Î¦

  âŸ…âˆ™âŸ†â†’ : (i : âˆ€ {x} â†’ ProofOf x â†’ ProofOf (f x))
       â†’ âŸ… Ï• âŸ† x â† t âŸ… Ïˆ x âŸ†
       â†’ âŸ… Ï• âŸ† x â† t âŸ… f <$> Ïˆ x âŸ†
  âŸ…âˆ™âŸ†â†’ i h =
    subst-âŸ…âŸ† (>>=-idÊ³ _) (seqâ€² Î¦ Î¨ (Î» x â†’ _ ,  sef-<$> f (Ïˆ x) (Î¨ x .snd .fst) , det-<$> f (Ïˆ x) (Î¨ x .snd .snd)) h
      Î» where x .proof k â†’
                            (do a â† Ïˆ x
                                b â† f <$> Ïˆ x
                                k _ (a |â†’| b)) â‰¡âŸ¨ a â‡ Ïˆ x â¨¾/ assoc (Ïˆ x) _ _ âŸ©

                            (do a â† Ïˆ x
                                b â† Ïˆ x
                                k _ (a |â†’| f b)) â‰¡âŸ¨ sef-dsef-cont (Î¨ x) _ âŸ©

                            (do a â† Ïˆ x
                                k _ (a |â†’| f a)) â‰¡âŸ¨ a â‡ Ïˆ x â¨¾/ cong (k _) (proves i) âŸ©

                            (do a â† Ïˆ x
                                k _ True) â‰¡Ë˜âŸ¨ Î¨ x .snd .fst _ âŸ©

                            (do a â† Ïˆ x
                                b â† Ïˆ x
                                k _ True) â‰¡Ë˜âŸ¨ a â‡ Ïˆ x â¨¾/ assoc (Ïˆ x) _ _ âŸ©

                            (do a â† Ïˆ x
                                b â† f <$> Ïˆ x
                                k _ True) âˆ
        )



FalseâŸ…â†’âŸ† : (t : Term A) (Ï• : A â†’ Term (Î© b)) â†’ âŸ… return (False {â„“ = c}) âŸ† x â† t âŸ… Ï• x âŸ†
FalseâŸ…â†’âŸ† t Ï• .proof k = x â‡ t â¨¾/ b â‡ Ï• x â¨¾/ cong (k x) (|â†’|-annË¡ _)

âŸ…idâŸ† : (Ï• : Term (Î© a)) â†’ DET (â„“suc a) Ï• â†’ âŸ… Ï• âŸ† pass {a = a} âŸ… Ï• âŸ†
âŸ…idâŸ† Ï• det .proof k = det (Î» a b â†’ k Poly-tt (a |â†’| b)) â¨¾ a â‡ Ï• â¨¾/ b â‡ Ï• â¨¾/ cong (k Poly-tt) (|â†’|-id _)

âŸ…retâŸ† : (Ï• : Î© a) (t : Term B) â†’ âŸ… return Ï• âŸ† _ â† t âŸ… return Ï• âŸ†
âŸ…retâŸ† Ï• t .proof k = x â‡ t â¨¾/ cong (k x) (|â†’|-id Ï•)

â‰¡âŸ…âˆ™âŸ†â‰¡ : {Ï• Ï•â€² : Term (Î© a)} {t : Term A} {Ïˆ Ïˆâ€² : A â†’ Term (Î© b)} â†’ Ï• â‰¡ Ï•â€² â†’ (âˆ€ x â†’ Ïˆ x â‰¡ Ïˆâ€² x) â†’ âŸ… Ï• âŸ† x â† t âŸ… Ïˆ x âŸ† â†’ âŸ… Ï•â€² âŸ† x â† t âŸ… Ïˆâ€² x âŸ†
â‰¡âŸ…âˆ™âŸ†â‰¡ {t = t} p q = substâ‚‚ (Î» p q â†’ Hoare p t q) p (funExt q)

â‰¡âŸ…âˆ™âŸ† : {Ï• Ï•â€² : Term (Î© a)} {t : Term A} {Ïˆ : A â†’ Term (Î© b)} â†’ Ï• â‰¡ Ï•â€² â†’ âŸ… Ï• âŸ† x â† t âŸ… Ïˆ x âŸ† â†’ âŸ… Ï•â€² âŸ† x â† t âŸ… Ïˆ x âŸ†
â‰¡âŸ…âˆ™âŸ† {t = t} {Ïˆ = Ïˆ} = subst (Î» p â†’ Hoare p t Ïˆ)

âŸ…âˆ™âŸ†â‰¡ : {Ï• : Term (Î© a)} {t : Term A} {Ïˆ Ïˆâ€² : A â†’ Term (Î© b)} â†’ (âˆ€ x â†’ Ïˆ x â‰¡ Ïˆâ€² x) â†’ âŸ… Ï• âŸ† x â† t âŸ… Ïˆ x âŸ† â†’ âŸ… Ï• âŸ† x â† t âŸ… Ïˆâ€² x âŸ†
âŸ…âˆ™âŸ†â‰¡ {Ï• = Ï•} {t = t} e = subst (Î» p â†’ Hoare Ï• t p) (funExt e)

âŸ¨&&&âŸ©Ê³ :
  (Ï• : Term (Î© b))
  (Ïˆ : A â†’ Term (Î© b))
  (Ï‡ : Î© b)
  (t : Term A) â†’
  âŸ… Ï• âŸ† x â† t âŸ… Ïˆ x âŸ† â†’
  âŸ… Ï• âŸ¨âˆ§âŸ© return Ï‡ âŸ† x â† t âŸ… Ïˆ x âŸ¨âˆ§âŸ© return Ï‡ âŸ†
âŸ¨&&&âŸ©Ê³ Ï• Ïˆ Ï‡ t h = â‰¡âŸ…âˆ™âŸ†â‰¡ (a â‡ Ï• â¨¾/ cong return (âˆ§-com Ï‡ a)) (Î» x â†’ b â‡ Ïˆ x â¨¾/ cong return (âˆ§-com Ï‡ b)) (âŸ¨&&&âŸ©Ë¡ Ï• Ïˆ Ï‡ t h)

FalseâŸ¨âˆ§âŸ© : (Ï• : Term (Î© a)) â†’ SEF (â„“suc a) Ï• â†’ return False âŸ¨âˆ§âŸ© Ï• â‰¡ return False
FalseâŸ¨âˆ§âŸ© Ï• sef =
  return False âŸ¨âˆ§âŸ© Ï• â‰¡âŸ¨âŸ©
  (do f â† return False; p â† Ï•; return (f |âˆ§| p)) â‰¡âŸ¨âŸ©
  (do p â† Ï•; return (False |âˆ§| p)) â‰¡âŸ¨ p â‡ Ï• â¨¾/ cong return (fst iff Î» ()) âŸ©
  (do p â† Ï•; return False) â‰¡âŸ¨ sef (return False) âŸ©
  return False âˆ

TrueâŸ¨âˆ§âŸ© : (Ï• : Term (Î© a)) â†’ return True âŸ¨âˆ§âŸ© Ï• â‰¡ Ï•
TrueâŸ¨âˆ§âŸ© Ï• = (a â‡ Ï• â¨¾/ cong return (âˆ§-id a)) â¨¾ <$>-id Ï•


weaken : {A : Type a}
       â†’ (Î¦ : Assertion a) â†’ let Ï• = Î¦ .fst in
         (Î¨ : A â†’ Assertion a) â†’ let Ïˆ = fst âˆ˜ Î¨ in
         {p : Term A}
       â†’ (Î§ : Assertion a) â†’ let Ï‡ = Î§ .fst in
         âŸ… Ï‡ âŸ† pass âŸ… Ï• âŸ†
       â†’ âŸ… Ï• âŸ† x â† p âŸ… Ïˆ x âŸ†
       â†’ âŸ… Ï‡ âŸ† x â† p âŸ… Ïˆ x âŸ†
weaken Î¦ Î¨ {p = p} Î§ lhs rhs =
  let h = seq Î§ (Î» _ â†’ Î¦) (Î» { (_ , x) â†’ Î¨ x }) lhs (Î» _ â†’ rhs)
      hâ€² = focus snd h
  in subst-âŸ…âŸ† (assoc (return Poly-tt) (Î» x â†’ p >>= Î» y â†’ return (x , y)) (return âˆ˜â€² snd) â¨¾ assoc p _ _ â¨¾ >>=-idÊ³ p) hâ€²

if : {A : Type a}
   â†’                    (Ï• : Term (Î© a)) (Ïˆ : A â†’ Term (Î© a)) 
   â†’
   (p : Term A) (q : Term A) (b : Bool) â†’
   âŸ… return (|T|     b)  âŸ¨âˆ§âŸ© Ï• âŸ† x â† p âŸ… Ïˆ x âŸ† â†’
   âŸ… return (|T| (!  b)) âŸ¨âˆ§âŸ© Ï• âŸ† x â† q âŸ… Ïˆ x âŸ† â†’    
   âŸ… Ï• âŸ† x â† if b then p else q âŸ… Ïˆ x âŸ†

if Ï• Ïˆ p q =
  bool 
    (Î» _ rhs â†’ subst (Î» e â†’ âŸ… e âŸ† x â† q âŸ… Ïˆ x âŸ†) (âˆ§-lemma Ï•) rhs)
    (Î» lhs _ â†’ subst (Î» e â†’ âŸ… e âŸ† x â† p âŸ… Ïˆ x âŸ†) (âˆ§-lemma Ï•) lhs)
    where
    âˆ§-lemma : (x : Term (Î© a)) â†’ (True |âˆ§|_) <$> x â‰¡ x
    âˆ§-lemma x = cong (_<$> x) (funExt âˆ§-id) â¨¾ <$>-id x

conj : {A : Type a}
     â†’                 (Î¦ : Assertion a) â†’ let Ï• = fst Î¦ in
                       (Ïˆ : A â†’ Term (Î© a)) â†’ (âˆ€ x â†’ SEF (â„“suc a) (Ïˆ x))
     â†’                 (Ï‡ : A â†’ Term (Î© a)) 
     â†’                              (p : Term A)
     â†’
      (âŸ… Ï• âŸ† x â† p âŸ… Ïˆ x âŸ†) â†’ (âŸ… Ï• âŸ† x â† p âŸ… Ï‡ x âŸ†) â†’ (âŸ… Ï• âŸ† x â† p âŸ… Ïˆ x âŸ¨âˆ§âŸ© Ï‡ x âŸ†)

conj {a = aâ„“} (Ï• , sefâŸ¨Ï•âŸ© , dsefâŸ¨Ï•âŸ©) Ïˆ sefâŸ¨ÏˆâŸ© Ï‡ p lhs rhs .proof k =

  (do a â† Ï•
      x â† p
      b â† (Ïˆ x âŸ¨âˆ§âŸ© Ï‡ x)
      k x (a |â†’| b)) â‰¡âŸ¨âŸ©

  (do a â† Ï•
      x â† p
      b â† (do bâ‚ â† Ïˆ x ; bâ‚‚ â† Ï‡ x ; return (bâ‚ |âˆ§| bâ‚‚))
      k x (a |â†’| b)) â‰¡âŸ¨ a â‡ Ï• â¨¾/ x â‡ p â¨¾/ assoc (Ïˆ x) _ _ â¨¾ bâ‚ â‡ Ïˆ x â¨¾/ assoc (Ï‡ x) _ _ âŸ©

  (do a â† Ï•
      x â† p
      bâ‚ â† Ïˆ x
      bâ‚‚ â† Ï‡ x
      k x (a |â†’| (bâ‚ |âˆ§| bâ‚‚)))

        â‰¡âŸ¨ a â‡ Ï• â¨¾/ x â‡ p â¨¾/ bâ‚ â‡ Ïˆ x â¨¾/ bâ‚‚ â‡ Ï‡ x â¨¾/ cong (k x) (|â†’|-|âˆ§| a bâ‚ bâ‚‚) âŸ©

  (do a â† Ï•
      x â† p
      bâ‚ â† Ïˆ x
      bâ‚‚ â† Ï‡ x
      k x ((a |â†’| bâ‚) |âˆ§| (a |â†’| bâ‚‚)))

      â‰¡Ë˜âŸ¨ sef-dsef-cont (Ï• , sefâŸ¨Ï•âŸ© , dsefâŸ¨Ï•âŸ©) _ âŸ©

  (do aâ‚ â† Ï•
      aâ‚‚ â† Ï•
      x â† p
      bâ‚ â† Ïˆ x
      bâ‚‚ â† Ï‡ x
      k x ((aâ‚‚ |â†’| bâ‚) |âˆ§| (aâ‚ |â†’| bâ‚‚)))

      â‰¡âŸ¨ aâ‚ â‡ Ï• â¨¾/ lhs .proof (Î» x r â†’ do bâ‚‚ â† Ï‡ x ; k x (r |âˆ§| (aâ‚ |â†’| bâ‚‚))) âŸ©

  (do aâ‚ â† Ï•
      aâ‚‚ â† Ï•
      x â† p
      Ïˆ x
      bâ‚‚ â† Ï‡ x
      k x (True {aâ„“} |âˆ§| (aâ‚ |â†’| bâ‚‚))) â‰¡âŸ¨ sef-dsef-cont (Ï• , sefâŸ¨Ï•âŸ© , dsefâŸ¨Ï•âŸ©) _ âŸ©

  (do a â† Ï•
      x â† p
      Ïˆ x
      bâ‚‚ â† Ï‡ x
      k x (True {aâ„“} |âˆ§| (a |â†’| bâ‚‚)))

        â‰¡âŸ¨ a â‡ Ï• â¨¾/ x â‡ p â¨¾/ _ â‡ Ïˆ x â¨¾/ bâ‚‚ â‡ Ï‡ x â¨¾/ cong (k x) (âˆ§-id _) âŸ©

  (do a â† Ï•
      x â† p
      Ïˆ x
      bâ‚‚ â† Ï‡ x
      k x (a |â†’| bâ‚‚)) â‰¡âŸ¨ a â‡ Ï• â¨¾/ x â‡ p â¨¾/ sefâŸ¨ÏˆâŸ© x _ âŸ©

  (do a â† Ï•
      x â† p
      bâ‚‚ â† Ï‡ x
      k x (a |â†’| bâ‚‚)) â‰¡âŸ¨ rhs .proof k âŸ©

  (do a â† Ï•
      x â† p
      Ï‡ x
      k x True) â‰¡Ë˜âŸ¨ a â‡ Ï• â¨¾/ x â‡ p â¨¾/ sefâŸ¨ÏˆâŸ© x _ âŸ©

  (do a â† Ï•
      x â† p
      Ïˆ x
      Ï‡ x
      k x True) â‰¡Ë˜âŸ¨ a â‡ Ï• â¨¾/ x â‡ p â¨¾/ assoc (Ïˆ x) _ _ â¨¾ b â‡ Ïˆ x â¨¾/ assoc (Ï‡ x) _ _ âŸ©

  (do a â† Ï•
      x â† p
      Ïˆ x âŸ¨âˆ§âŸ© Ï‡ x
      k x True) âˆ

âŸ…âˆ§âŸ† : {A : Type a} {p : Î© a} (Ï• : Assertion a) {t : Term A} (Ïˆ : A â†’ Assertion a)
   â†’ âŸ… Ï• .fst âŸ† x â† t âŸ… Ïˆ x .fst âŸ† â†’ âŸ… return p âŸ¨âˆ§âŸ© Ï• .fst âŸ† x â† t âŸ… return p âŸ¨âˆ§âŸ© Ïˆ x .fst âŸ†
âŸ…âˆ§âŸ† {p = p} Ï• {t = t} Ïˆ rhs =
  conj
    (return p âŸ¨âˆ§âŸ© Ï• .fst , sef-<$> _ _ (Ï• .snd .fst) , det-<$> _ _ (Ï• .snd .snd))
    (Î» _ â†’ return p)
    ( Î» x k â†’ refl)
    (Î» x â†’ Ïˆ x .fst)
    t
    (Î» where .proof k â†’ (do a â† return p âŸ¨âˆ§âŸ© Ï• .fst; x â† t; b â† return p; k x (a |â†’| b)) â‰¡âŸ¨ assoc (Ï• .fst) _ _ âŸ©
                        (do a â† Ï• .fst; x â† t; b â† return p; k x ((p |âˆ§| a) |â†’| b)) â‰¡âŸ¨âŸ©
                        (do a â† Ï• .fst; x â† t; k x ((p |âˆ§| a) |â†’| p)) â‰¡âŸ¨ a â‡ Ï• .fst â¨¾/ x â‡ t â¨¾/ cong (k x) (proves fst) âŸ©
                        (do a â† Ï• .fst; x â† t; k x True) â‰¡Ë˜âŸ¨ assoc (Ï• .fst) _ _ âŸ©
                        (do a â† return p âŸ¨âˆ§âŸ© Ï• .fst; x â† t; b â† return p; k x True) âˆ
    )
    (â†’âŸ…âˆ™âŸ† (_ , Ï• .snd .fst ,  Ï• .snd .snd) _ snd rhs)

module _ {A : Type a} where
  disj :                    (Ï• : Term (Î© a))
       â†’                    (Ïˆ : Term (Î© a)) â†’ SEF (â„“suc a) Ïˆ
       â†’                    (Î§ : A â†’ Assertion a) â†’ let Ï‡ = fst âˆ˜ Î§ in 
                            (p : Term A)
       â†’
       (âŸ… Ï• âŸ† x â† p âŸ… Ï‡ x âŸ†) â†’ (âŸ… Ïˆ âŸ† x â† p âŸ… Ï‡ x âŸ†) â†’ (âŸ… Ï• âŸ¨âˆ¨âŸ© Ïˆ âŸ† x â† p âŸ… Ï‡ x âŸ†)
  disj Ï• Ïˆ sefâŸ¨ÏˆâŸ© Î§ p lhs rhs .proof k = let Ï‡ = fst âˆ˜ Î§ in
    (do a â† Ï• âŸª _|âˆ¨|_ âŸ« Ïˆ
        x â† p
        b â† Ï‡ x
        k x (a |â†’| b)) â‰¡âŸ¨ âŸªassocâŸ« Ï• Ïˆ _|âˆ¨|_ _ âŸ©
  
    (do aâ‚ â† Ï•
        aâ‚‚ â† Ïˆ
        x â† p
        b â† Ï‡ x
        k x (aâ‚ |âˆ¨| aâ‚‚ |â†’| b))
  
          â‰¡âŸ¨ aâ‚ â‡ Ï• â¨¾/ aâ‚‚ â‡ Ïˆ â¨¾/ x  â‡ p â¨¾/ b  â‡ Ï‡ x â¨¾/ cong (k x) (|âˆ¨|-|â†’| _ _ _) âŸ©
  
    (do aâ‚ â† Ï•
        aâ‚‚ â† Ïˆ
        x â† p
        b â† Ï‡ x
        k x ((aâ‚ |â†’| b) |âˆ§| (aâ‚‚ |â†’| b))) â‰¡Ë˜âŸ¨ aâ‚ â‡ Ï• â¨¾/ aâ‚‚ â‡ Ïˆ â¨¾/ x â‡ p â¨¾/ sef-dsef-cont (Î§ x) _ âŸ©
  
    (do aâ‚ â† Ï•
        aâ‚‚ â† Ïˆ
        x â† p
        bâ‚ â† Ï‡ x
        bâ‚‚ â† Ï‡ x
        k x ((aâ‚ |â†’| bâ‚‚) |âˆ§| (aâ‚‚ |â†’| bâ‚))) â‰¡âŸ¨ aâ‚ â‡ Ï• â¨¾/ rhs .proof (Î» x r â†’ Ï‡ x >>= Î» bâ‚‚ â†’ k x ((aâ‚ |â†’| bâ‚‚) |âˆ§| r)) âŸ©
  
    (do aâ‚ â† Ï•
        aâ‚‚ â† Ïˆ
        x â† p
        bâ‚ â† Ï‡ x
        bâ‚‚ â† Ï‡ x
        k x ((aâ‚ |â†’| bâ‚‚) |âˆ§| True)) â‰¡âŸ¨ aâ‚ â‡ Ï• â¨¾/ sefâŸ¨ÏˆâŸ© _ âŸ©
  
    (do a â† Ï•
        x â† p
        bâ‚ â† Ï‡ x
        bâ‚‚ â† Ï‡ x
        k x ((a |â†’| bâ‚‚) |âˆ§| True)) â‰¡âŸ¨ a â‡ Ï• â¨¾/ x â‡ p â¨¾/ sef-dsef-cont (Î§ x) _ âŸ©
  
    (do a â† Ï•
        x â† p
        b â† Ï‡ x
        k x ((a |â†’| b) |âˆ§| True)) â‰¡âŸ¨ a â‡ Ï• â¨¾/ x â‡ p â¨¾/ b â‡ Ï‡ x â¨¾/ cong (k x) (âˆ§-com _ _ â¨¾ âˆ§-id _) âŸ©
  
    (do a â† Ï•
        x â† p
        b â† Ï‡ x
        k x (a |â†’| b)) â‰¡âŸ¨ lhs .proof k âŸ©
  
    (do a â† Ï•
        x â† p
        b â† Ï‡ x
        k x True) â‰¡Ë˜âŸ¨ a â‡ Ï• â¨¾/ sefâŸ¨ÏˆâŸ© _ âŸ©
  
    (do aâ‚ â† Ï•
        aâ‚‚ â† Ïˆ
        x â† p
        b â† Ï‡ x
        k x True) â‰¡Ë˜âŸ¨ âŸªassocâŸ« Ï• Ïˆ _|âˆ¨|_ _ âŸ©
  
    (do a â† Ï• âŸª _|âˆ¨|_ âŸ« Ïˆ
        x â† p
        b â† Ï‡ x
        k x True) âˆ
